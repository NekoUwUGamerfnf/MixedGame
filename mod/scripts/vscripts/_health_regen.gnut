
global function HealthRegen_Init

global function PilotHealthRegenThinkSP
global function PilotShieldHealthUpdate

// modified
global function HealthRegen_SetCustomRegenDelay
global function HealthRegen_SetCustomRegenRate
global function HealthRegen_SetAlwaysFastRegen
global function HealthRegen_Disabled

struct
{
	float healthRegenRate

	//modified
	float customHealthRegenDelayNormal = -1.0
	float customHealthRegenDelayFast = -1.0
	float customHealthRegenDelayStim = -1.0
	float customHealthRegenRateNormal = -1.0
	float customHealthRegenRateStim = -1.0
	bool alwaysFastRegen = false
	bool disableHealthRegen = false

	// damage feedback variable
	bool isShieldDamage = false
	float shieldActualDamage = 0.0
	bool isShieldBrokeDamage = false
	float shieldBrokeActualDamage = 0.0

	// in-file tables
	table<entity, float> playerNextShieldDamageSoundAllowedTime
} file

void function HealthRegen_Init()
{
	if ( IsSingleplayer() )
	{
		file.healthRegenRate = 1.0
	}
	else
	{
		file.healthRegenRate = 6.0

		AddCallback_PlayerClassChanged( HealthRegen_OnPlayerClassChangedMP )
		RegisterSignal( "PilotHealthRegenThink" )
		RegisterSignal( "StopHealthRegenThink" ) // modify for having different health regen function for each player

		// modified callback in _base_gametype.gnut
		// because PlayerDamageFeedbackStruct definition is in _base_gametype.gnut, which gets compiled later than this file
		// we have to wrap this callback inside
		AddCallback_PlayerDamageFeedback(
			void function( entity victim, var damageInfo, PlayerDamageFeedbackStruct feedbackStruct )
			{
				if ( file.isShieldDamage )
				{
					feedbackStruct.damageToDisplay = file.shieldActualDamage
					file.isShieldDamage = false
				}
				if ( file.isShieldBrokeDamage ) // for pilot taking a damage that will break shield, display the correct damage amount
				{
					//print( "pilot taking shield broke damage!" )
					feedbackStruct.damageToDisplay = file.shieldBrokeActualDamage
					file.isShieldBrokeDamage = false
				}
			}
		)
		// modified to handle in-file table
		AddCallback_OnClientConnected( HealthRegen_OnClientConnected )
	}
}

void function PilotHealthRegenThinkSP( entity player )
{
	player.EndSignal( "OnDestroy" )

	while ( IsValid( player ) )
	{
		wait( HEALTH_REGEN_TICK_TIME )

		if ( !IsAlive( player ) )
			continue

		if ( !IsPilot( player ) )
			continue

		if ( shGlobal.proto_pilotHealthRegenDisabled )
			continue

		//Assert( IsTestMap() || player.GetPlayerSettings() == DEFAULT_PILOT_SETTINGS, "for now, we should all be pilot_solo at all times, or in a test map." )

		if ( player.GetHealth() == player.GetMaxHealth() )
			continue

		float healthRegenRate = 4.0
		float healthRegenStartDelay = GraphCapped( player.GetHealth(), 0, player.GetMaxHealth(), 3.0, 0.8 )

		//printt( "recentDamage " + recentDamage + " delay " + healthRegenStartDelay + " rate " + healthRegenRate )

		if ( Time() - player.p.lastDamageTime < healthRegenStartDelay )
		{
			continue
		}

		player.SetHealth( min( player.GetMaxHealth(), player.GetHealth() + healthRegenRate ) )
	}
}

bool function IsHealActive( entity player )
{
	return StatusEffect_Get( player, eStatusEffect.stim_visual_effect ) > 0.0
}

void function PilotHealthRegenThinkMP( entity player )
{
	player.EndSignal( "OnDestroy" )
	player.Signal( "PilotHealthRegenThink" )
	player.EndSignal( "PilotHealthRegenThink" )
	player.EndSignal( "StopHealthRegenThink" ) // modify for having different health regen function for each player

	float healthRegenStartDelay = 5.0 //Needs to use GetPlayerSettingsField() instead of hard coding, waiting on Bug 129567
	//float healthRegenStartDelay = player.GetPlayerSettingsField( "powerRegenRateOp" ) // seconds after we take damager to start regen

	// why do we cache these things here? what if we update passives mid-way??
	//if ( PlayerHasPassive( player, ePassives.PAS_FAST_HEALTH_REGEN ) )
	//	healthRegenStartDelay = 2.5

	while ( IsValid( player ) )
	{
		wait( HEALTH_REGEN_TICK_TIME )

		// modified settings!
		if( file.disableHealthRegen )
			continue

		if ( !IsAlive( player ) )
			continue

		if ( !IsPilot( player ) )
			continue

		if ( shGlobal.proto_pilotHealthRegenDisabled )
			continue

		float healthRegenRate = file.healthRegenRate 	// health regen per tick
		// modified settings!!!
		if ( file.customHealthRegenRateNormal > 0 )
			healthRegenRate = file.customHealthRegenRateNormal

		if ( player.GetHealth() == player.GetMaxHealth() )
			continue

		// No regen during phase shift
		if ( player.IsPhaseShifted() )
			continue

		// modified: update health regen delay each loop
		healthRegenStartDelay = 5.0
		if ( PlayerHasPassive( player, ePassives.PAS_FAST_HEALTH_REGEN ) || file.alwaysFastRegen )
			healthRegenStartDelay = 2.5

		// modified settings!!!
		if ( file.customHealthRegenDelayNormal > 0 )
			healthRegenStartDelay = file.customHealthRegenDelayNormal
		if ( file.customHealthRegenDelayFast > 0 )
		{
			if ( PlayerHasPassive( player, ePassives.PAS_FAST_HEALTH_REGEN ) || file.alwaysFastRegen )
				healthRegenStartDelay = file.customHealthRegenDelayFast
		}

		if ( IsHealActive( player ) )
		{
			// modified settings!!!
			if( file.customHealthRegenDelayStim > 0 )
			{
				if ( Time() - player.p.lastDamageTime < min( file.customHealthRegenDelayStim, healthRegenStartDelay ) )
					continue
				else
				{
					if ( file.customHealthRegenRateStim > 0 )
						healthRegenRate = file.customHealthRegenRateStim
					else
						healthRegenRate = healthRegenRate * ABILITY_STIM_REGEN_MOD
				}
			}
			else
			{
				if ( Time() - player.p.lastDamageTime < min( ABILITY_STIM_REGEN_DELAY, healthRegenStartDelay ) )
					continue
				else
				{
					// modified settings!!!
					if ( file.customHealthRegenRateStim > 0 )
						healthRegenRate = file.customHealthRegenRateStim
					else
						healthRegenRate = healthRegenRate * ABILITY_STIM_REGEN_MOD
				}
			}
		}
		else if ( Time() - player.p.lastDamageTime < healthRegenStartDelay )
		{
			continue
		}

		player.SetHealth( min( player.GetMaxHealth(), player.GetHealth() + healthRegenRate ) )
		if ( player.GetHealth() == player.GetMaxHealth() )
		{
			ClearRecentDamageHistory( player )
			ClearLastAttacker( player )
		}
	}
}

void function HealthRegen_OnPlayerClassChangedMP( entity player )
{
	thread PilotHealthRegenThinkMP( player )
}

// APEX version
float function PilotShieldHealthUpdate( entity player, var damageInfo )
{
	//print( "Running PilotShieldHealthUpdate()" )
	if ( DamageInfo_GetForceKill( damageInfo ) )
	{
		player.SetShieldHealth( 0 )
		return 0.0
	}

	int shieldHealth = player.GetShieldHealth()

	float shieldDamage = 0

	if ( shieldHealth )
	{
		DamageInfo_AddCustomDamageType( damageInfo, DF_SHIELD_DAMAGE )

		shieldDamage = PilotShieldModifyDamage( player, damageInfo )

		//if ( shieldDamage )
		//	DamageInfo_SetDamage( damageInfo, shieldDamage )
	}

	return shieldDamage
}

// modified to handle in-file table
void function HealthRegen_OnClientConnected( entity player )
{
	// init everything
	file.playerNextShieldDamageSoundAllowedTime[ player ] <- 0.0 // always valid on next hit
}

// from PR#65(https://github.com/DBmaoha/Super.Mixed.Game/pull/65)
// vanilla missing visual effect consts
const string PILOT_SHIELD_DAMAGE_SOUND = "titanshield_light_bulletimpact_1p_vs_3p"
const string PILOT_SHIELD_DAMAGE_SOUND_BULLET = "titanshieldwall_light_bulletimpact_1p_vs_3p" // getting hit by bullets plays a different sound
const float PILOT_SHIELD_DAMAGE_SOUND_INTERVAL_MIN = 0.25 // prevent sound spaming to player
const float PILOT_SHIELD_DAMAGE_SOUND_INTERVAL_MAX = 0.35

const float PILOT_SHIELD_DAMAGE_EFFECT_SEVERITY_MIN = 0.2
const float PILOT_SHIELD_DAMAGE_EFFECT_SEVERITY_MAX = 0.6
const float PILOT_SHIELD_DAMAGE_EFFECT_DURATION_MIN = 1.0
const float PILOT_SHIELD_DAMAGE_EFFECT_DURATION_MAX = 2.0
const float PILOT_SHIELD_DAMAGE_EFFECT_FADEOUT_DURATION = 1.0

const float PILOT_SHIELD_BROKE_EFFECT_SEVERITY = 1.0
const float PILOT_SHIELD_BROKE_EFFECT_DURATION = 1.0
const float PILOT_SHIELD_BROKE_EFFECT_FADEOUT_DURATION = 0.5

const bool SHOUND_SEND_PILOT_SHIELD_DAMAGE_MESSAGE = true
const string PILOT_SHIELD_DAMAGE_MESSAGE = "护盾受击"
const string PILOT_SHIELD_BROKE_MESSAGE = "护盾损坏"

float function PilotShieldModifyDamage( entity player, var damageInfo )
{
	float shieldHealth = float( player.GetShieldHealth() )
	float shieldHealthMax = float( player.GetShieldHealthMax() )
	float damage = DamageInfo_GetDamage( damageInfo )

	float newShieldHealth = shieldHealth - damage
	float permanentDamage = 0.0

	if ( newShieldHealth < 0 )
		permanentDamage = fabs( newShieldHealth )

	player.SetShieldHealth( maxint( 0, int( newShieldHealth ) ) )

	entity attacker = DamageInfo_GetAttacker( damageInfo )

	// shield on-hit sound, from PR#65
	if ( Time() > file.playerNextShieldDamageSoundAllowedTime[ player ] )
	{
		string shieldHitSound = PILOT_SHIELD_DAMAGE_SOUND
		if ( DamageInfo_GetCustomDamageType( damageInfo ) & DF_BULLET ) // bullet damage specific sound
			shieldHitSound = PILOT_SHIELD_DAMAGE_SOUND_BULLET
		EmitSoundOnEntityOnlyToPlayer( player, player, shieldHitSound )

		// add sound cooldown
		float nextSoundInterval = RandomFloatRange( PILOT_SHIELD_DAMAGE_SOUND_INTERVAL_MIN, PILOT_SHIELD_DAMAGE_SOUND_INTERVAL_MAX )
		file.playerNextShieldDamageSoundAllowedTime[ player ] = Time() + nextSoundInterval
	}
	//else
	//	print( "player still in shield damage sound cooldown!" )

	if ( shieldHealth && newShieldHealth <= 0 ) // shield cracked!
	{
		// vanilla behavior( actually no where used until I modify it )
		//EmitSoundOnEntity( player, "titan_energyshield_down" )

		// from PR#65
		// add better visual and sound effect
		EmitSoundOnEntityOnlyToPlayer( player, player, "titan_energyshield_down_1P" ) // play 1p crack sound!!
		EmitSoundOnEntityExceptToPlayer( player, player, "titan_energyshield_down" ) // 3p crack sound to others

		StatusEffect_AddTimed( player, eStatusEffect.emp, PILOT_SHIELD_BROKE_EFFECT_SEVERITY, PILOT_SHIELD_BROKE_EFFECT_DURATION, PILOT_SHIELD_BROKE_EFFECT_FADEOUT_DURATION )

		if( SHOUND_SEND_PILOT_SHIELD_DAMAGE_MESSAGE )
			SendHudMessage( player, PILOT_SHIELD_BROKE_MESSAGE +"\n0/"+ shieldHealthMax, -1, -0.4, 255, 0, 0, 255, 0, 1.0, 1.0 )
	}
	if ( permanentDamage > 0 ) // shield cracked and player taking health damage
	{
		//print( "Shield Modified damage is: " + string( permanentDamage ) )
		file.isShieldBrokeDamage = true
		file.shieldBrokeActualDamage = damage
		DamageInfo_SetDamage( damageInfo, permanentDamage )

		//if ( permanentDamage >= player.GetHealth() ) // defensive fix, now handled in _codecallbacks.gnut
		//	player.SetShieldHealth( 0 )
	}
	else // damage is protected
	{
		//print( "Damage protected by pilot shield!" )
		file.isShieldDamage = true
		file.shieldActualDamage = damage
		DamageInfo_SetDamage( damageInfo, 0 )

		// from PR#65
		// add better visual and sound effect
		// effect severity is based on damage amount, which means when player gets high damage, the effect hits harder to alert them
		// effect duration is based on player's current shield / max shield health
		// which means when player having low shield, the effect hits harder to alert them
		if( newShieldHealth > 0 )	// make sure shield is not broke, because if shield is broke means we have played broke effect before
		{
			float effectSeverity = GraphCapped( damage, 0, shieldHealthMax / 2, PILOT_SHIELD_DAMAGE_EFFECT_SEVERITY_MIN, PILOT_SHIELD_DAMAGE_EFFECT_SEVERITY_MAX )
			float effectDuration = GraphCapped( newShieldHealth, shieldHealthMax, 0, PILOT_SHIELD_DAMAGE_EFFECT_DURATION_MIN, PILOT_SHIELD_DAMAGE_EFFECT_DURATION_MAX )
			float fadeoutDuration = PILOT_SHIELD_DAMAGE_EFFECT_FADEOUT_DURATION

			StatusEffect_AddTimed( player, eStatusEffect.emp, effectSeverity, effectDuration, fadeoutDuration )

			if( SHOUND_SEND_PILOT_SHIELD_DAMAGE_MESSAGE )
			{
				// let the colors more red if player shield is low
				int msgColorGreen = int( GraphCapped( newShieldHealth, shieldHealthMax, 0, 255, 0 ) )
				SendHudMessage( player, PILOT_SHIELD_DAMAGE_MESSAGE +"\n"+ newShieldHealth +"/"+ shieldHealthMax, -1, -0.4, 255, msgColorGreen, 0, 255, 0, 1.0, 1.0 )
			}
		}
	}

	return min( shieldHealth, damage )
}

/*
float function PilotShieldHealthUpdate( entity player, var damageInfo )
{
	if ( DamageInfo_GetForceKill( damageInfo ) )
	{
		player.SetShieldHealth( 0 )
		return 0.0
	}

	int shieldHealth = player.GetShieldHealth()

	//float shieldDamage = 0
	float permanentDamage = 0.0

	if ( shieldHealth )
	{
		DamageInfo_AddCustomDamageType( damageInfo, DF_SHIELD_DAMAGE )

		//shieldDamage = PilotShieldModifyDamage( player, damageInfo )
		permanentDamage = PilotShieldModifyDamage( player, damageInfo )

		//if ( shieldDamage )
		//	DamageInfo_SetDamage( damageInfo, 0 )
		if( !permanentDamage )
			DamageInfo_SetDamage( damageInfo, 0 )
	}

	//return shieldDamage
	return permanentDamage
}

float function PilotShieldModifyDamage( entity player, var damageInfo )
{
	float shieldHealth = float( player.GetShieldHealth() )
	float damage = DamageInfo_GetDamage( damageInfo )

	float newShieldHealth = shieldHealth - damage
	float permanentDamage = 0.0

	//if ( newShieldHealth < 0 )
		//permanentDamage = fabs( newShieldHealth )
	if ( newShieldHealth < 0 )
		permanentDamage = fabs( newShieldHealth ) - shieldHealth

	player.SetShieldHealth( maxint( 0, int( newShieldHealth ) ) )

	if ( shieldHealth && newShieldHealth <= 0 )
	{
		EmitSoundOnEntity( player, "titan_energyshield_down" )
	}

	DamageInfo_SetDamage( damageInfo, permanentDamage )

	//return min( shieldHealth, damage )
	return permanentDamage
}
*/

// modified
void function HealthRegen_SetCustomRegenDelay( float normalRegenDelay, float fastRegenDelay, float stimRegenDelay )
{
	file.customHealthRegenDelayNormal = normalRegenDelay
	file.customHealthRegenDelayFast = fastRegenDelay
	file.customHealthRegenDelayStim = stimRegenDelay
}

void function HealthRegen_SetCustomRegenRate( float normalRegenRate, float stimRegenRate )
{
	file.customHealthRegenRateNormal = normalRegenRate
	file.customHealthRegenRateStim = stimRegenRate
}

void function HealthRegen_SetAlwaysFastRegen( bool shouldAlways )
{
	file.alwaysFastRegen = shouldAlways
}

void function HealthRegen_Disabled( bool disabled )
{
	file.disableHealthRegen = disabled
}