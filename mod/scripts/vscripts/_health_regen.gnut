
global function HealthRegen_Init

global function PilotHealthRegenThinkSP
global function PilotShieldHealthUpdate

// modified
global function SetUseCustomHealthRegenStat
global function SetCustomHealthRegenDelay
global function SetCustomHealthRegenRate
global function SetShouldAlwaysFastRegen
global function SetHealthRegenDisabled

// APEX
//global bool isPermanentDmg = false
//global float sPermanentDmg = 0.0

struct
{
	float healthRegenRate

	//modified
	bool useCustomHealthRegenStat = false
	float customHealthRegenDelayNormal
	float customHealthRegenDelayFast
	float customHealthRegenDelayStim
	float customHealthRegenRateNormal
	float customHealthRegenRateStim
	bool alwaysFastRegen = false
	bool disableHealthRegen = false
} file

void function HealthRegen_Init()
{
	if ( IsSingleplayer() )
	{
		file.healthRegenRate = 1.0
	}
	else
	{
		file.healthRegenRate = 6.0
		if( file.useCustomHealthRegenStat )
			file.healthRegenRate = file.customHealthRegenRateNormal

		AddCallback_PlayerClassChanged( HealthRegen_OnPlayerClassChangedMP )
		RegisterSignal( "PilotHealthRegenThink" )
		RegisterSignal( "StopHealthRegenThink" ) // modify for having different health regen function for each player
	}
}

void function PilotHealthRegenThinkSP( entity player )
{
	player.EndSignal( "OnDestroy" )

	while ( IsValid( player ) )
	{
		wait( HEALTH_REGEN_TICK_TIME )

		if ( !IsAlive( player ) )
			continue

		if ( !IsPilot( player ) )
			continue

		if ( shGlobal.proto_pilotHealthRegenDisabled )
			continue

		//Assert( IsTestMap() || player.GetPlayerSettings() == DEFAULT_PILOT_SETTINGS, "for now, we should all be pilot_solo at all times, or in a test map." )

		if ( player.GetHealth() == player.GetMaxHealth() )
			continue

		float healthRegenRate = 4.0
		float healthRegenStartDelay = GraphCapped( player.GetHealth(), 0, player.GetMaxHealth(), 3.0, 0.8 )

		//printt( "recentDamage " + recentDamage + " delay " + healthRegenStartDelay + " rate " + healthRegenRate )

		if ( Time() - player.p.lastDamageTime < healthRegenStartDelay )
		{
			continue
		}

		player.SetHealth( min( player.GetMaxHealth(), player.GetHealth() + healthRegenRate ) )
	}
}

bool function IsHealActive( entity player )
{
	return StatusEffect_Get( player, eStatusEffect.stim_visual_effect ) > 0.0
}

void function PilotHealthRegenThinkMP( entity player )
{
	player.EndSignal( "OnDestroy" )
	player.Signal( "PilotHealthRegenThink" )
	player.EndSignal( "PilotHealthRegenThink" )
	player.EndSignal( "StopHealthRegenThink" ) // modify for having different health regen function for each player

	float healthRegenStartDelay = 5.0 //Needs to use GetPlayerSettingsField() instead of hard coding, waiting on Bug 129567
	//float healthRegenStartDelay = player.GetPlayerSettingsField( "powerRegenRateOp" ) // seconds after we take damager to start regen
	if( file.useCustomHealthRegenStat )
	{
		healthRegenStartDelay = file.customHealthRegenDelayNormal
		if ( PlayerHasPassive( player, ePassives.PAS_FAST_HEALTH_REGEN ) || file.alwaysFastRegen )
			healthRegenStartDelay = file.customHealthRegenDelayFast
	}
	
	if ( PlayerHasPassive( player, ePassives.PAS_FAST_HEALTH_REGEN ) )
		healthRegenStartDelay = 2.5

	while ( IsValid( player ) )
	{
		wait( HEALTH_REGEN_TICK_TIME )

		if( file.disableHealthRegen )
			continue

		if ( !IsAlive( player ) )
			continue

		if ( !IsPilot( player ) )
			continue

		if ( shGlobal.proto_pilotHealthRegenDisabled )
			continue

		float healthRegenRate = file.healthRegenRate 	// health regen per tick

		if ( player.GetHealth() == player.GetMaxHealth() )
			continue

		// No regen during phase shift
		if ( player.IsPhaseShifted() )
			continue

		if ( IsHealActive( player ) )
		{
			if( file.useCustomHealthRegenStat )
			{
				if ( Time() - player.p.lastDamageTime < min( file.customHealthRegenDelayStim, healthRegenStartDelay ) )
					continue
				else
					healthRegenRate = file.customHealthRegenRateStim
			}
			else
			{
				if ( Time() - player.p.lastDamageTime < min( ABILITY_STIM_REGEN_DELAY, healthRegenStartDelay ) )
					continue
				else
					healthRegenRate = healthRegenRate * ABILITY_STIM_REGEN_MOD
			}
		}
		else if ( Time() - player.p.lastDamageTime < healthRegenStartDelay )
		{
			continue
		}

		player.SetHealth( min( player.GetMaxHealth(), player.GetHealth() + healthRegenRate ) )
		if ( player.GetHealth() == player.GetMaxHealth() )
		{
			ClearRecentDamageHistory( player )
			ClearLastAttacker( player )
		}
	}
}

void function HealthRegen_OnPlayerClassChangedMP( entity player )
{
	thread PilotHealthRegenThinkMP( player )
}

// APEX version
float function PilotShieldHealthUpdate( entity player, var damageInfo )
{
	if ( DamageInfo_GetForceKill( damageInfo ) )
	{
		player.SetShieldHealth( 0 )
		return 0.0
	}

	int shieldHealth = player.GetShieldHealth()

	float shieldDamage = 0

	if ( shieldHealth )
	{
		DamageInfo_AddCustomDamageType( damageInfo, DF_SHIELD_DAMAGE )

		shieldDamage = PilotShieldModifyDamage( player, damageInfo )

	//	if ( shieldDamage )
		//	DamageInfo_SetDamage( damageInfo, shieldDamage )
	}

	return shieldDamage
}

float function PilotShieldModifyDamage( entity player, var damageInfo )
{

	float shieldHealth = float( player.GetShieldHealth() )
	float damage = DamageInfo_GetDamage( damageInfo )

	float newShieldHealth = shieldHealth - damage
	float permanentDamage = 0.0

	if ( newShieldHealth < 0 )
		permanentDamage = fabs( newShieldHealth )

	player.SetShieldHealth( maxint( 0, int( newShieldHealth ) ) )

	entity attacker = DamageInfo_GetAttacker( damageInfo )

	if ( shieldHealth && newShieldHealth <= 0 )
	{
		EmitSoundOnEntity( player, "titan_energyshield_down" )
	}
	if(permanentDamage > 0)
	{
		//print( "Shield Modified damage is: " + string( permanentDamage ) )
		//isPermanentDmg = true
		//sPermanentDmg = damage
		DamageInfo_SetDamage( damageInfo, permanentDamage )

		if( permanentDamage >= player.GetHealth() ) // defensive fix
			player.SetShieldHealth( 0 )
	}
	else
		DamageInfo_SetDamage( damageInfo, 0 ) // damage is protected

	return min( shieldHealth, damage )
}

/*
float function PilotShieldHealthUpdate( entity player, var damageInfo )
{
	if ( DamageInfo_GetForceKill( damageInfo ) )
	{
		player.SetShieldHealth( 0 )
		return 0.0
	}

	int shieldHealth = player.GetShieldHealth()

	//float shieldDamage = 0
	float permanentDamage = 0.0

	if ( shieldHealth )
	{
		DamageInfo_AddCustomDamageType( damageInfo, DF_SHIELD_DAMAGE )

		//shieldDamage = PilotShieldModifyDamage( player, damageInfo )
		permanentDamage = PilotShieldModifyDamage( player, damageInfo )

		//if ( shieldDamage )
		//	DamageInfo_SetDamage( damageInfo, 0 )
		if( !permanentDamage )
			DamageInfo_SetDamage( damageInfo, 0 )
	}

	//return shieldDamage
	return permanentDamage
}

float function PilotShieldModifyDamage( entity player, var damageInfo )
{
	float shieldHealth = float( player.GetShieldHealth() )
	float damage = DamageInfo_GetDamage( damageInfo )

	float newShieldHealth = shieldHealth - damage
	float permanentDamage = 0.0

	//if ( newShieldHealth < 0 )
		//permanentDamage = fabs( newShieldHealth )
	if ( newShieldHealth < 0 )
		permanentDamage = fabs( newShieldHealth ) - shieldHealth

	player.SetShieldHealth( maxint( 0, int( newShieldHealth ) ) )

	if ( shieldHealth && newShieldHealth <= 0 )
	{
		EmitSoundOnEntity( player, "titan_energyshield_down" )
	}

	DamageInfo_SetDamage( damageInfo, permanentDamage )

	//return min( shieldHealth, damage )
	return permanentDamage
}
*/

// modified
void function SetUseCustomHealthRegenStat( bool shouldUse )
{
	file.useCustomHealthRegenStat = shouldUse
}

void function SetCustomHealthRegenDelay( float normalRegenDelay, float fastRegenDelay, float stimRegenDelay )
{
	file.customHealthRegenDelayNormal = normalRegenDelay
	file.customHealthRegenDelayFast = fastRegenDelay
	file.customHealthRegenDelayStim = stimRegenDelay
}

void function SetCustomHealthRegenRate( float normalRegenRate, float stimRegenRate )
{
	file.customHealthRegenRateNormal = normalRegenRate
	file.customHealthRegenRateStim = stimRegenRate
}

void function SetShouldAlwaysFastRegen( bool shouldAlways )
{
	file.alwaysFastRegen = shouldAlways
}

void function SetHealthRegenDisabled( bool disabled )
{
	file.disableHealthRegen = disabled
}