global function MpTitanWeaponFireWave_Init

global function OnWeaponPrimaryAttack_titanweapon_fire_wave
#if SERVER
global function OnWeaponNpcPrimaryAttack_titanweapon_fire_wave
#endif

void function MpTitanWeaponFireWave_Init()
{
    
}

var function OnWeaponPrimaryAttack_titanweapon_fire_wave( entity weapon, WeaponPrimaryAttackParams attackParams )
{
	entity weaponOwner = weapon.GetWeaponOwner()
	if ( weaponOwner.IsPhaseShifted() )
		return 0

	bool shouldPredict = weapon.ShouldPredictProjectiles()
	#if CLIENT
		if ( !shouldPredict )
			return 1
	#endif

	const float FUSE_TIME = 99.0
	entity projectile = weapon.FireWeaponGrenade( attackParams.pos, attackParams.dir, < 0,0,0 >, FUSE_TIME, damageTypes.projectileImpact, damageTypes.explosive, shouldPredict, true, true )
	if ( IsValid( projectile ) )
	{
		entity owner = weapon.GetWeaponOwner()

		if ( owner.IsPlayer() )
			PlayerUsedOffhand( owner, weapon )

		#if SERVER
			projectile.ProjectileSetDamageSourceID( eDamageSourceId.mp_titanweapon_flame_ring ) // change damageSourceID
			thread BeginFireWave( projectile, attackParams )
		#endif
	}

	return weapon.GetWeaponSettingInt( eWeaponVar.ammo_min_to_fire )
}

#if SERVER
void function BeginFireWave( entity projectile, WeaponPrimaryAttackParams attackParams )
{
	projectile.EndSignal( "OnDestroy" )
	projectile.SetAbsOrigin( projectile.GetOrigin() )
	projectile.SetAbsAngles( projectile.GetAngles() )
	projectile.SetVelocity( Vector( 0, 0, 0 ) )
	projectile.StopPhysics()
	projectile.SetTakeDamageType( DAMAGE_NO )
	projectile.Hide()
	projectile.NotSolid()
	projectile.e.onlyDamageEntitiesOnce = true
	// modded titan: stryder_fire_wave
	array<string> mods = projectile.ProjectileGetMods()
    EmitSoundOnEntity( projectile, "flamewall_flame_start" )
    waitthread WeaponAttackWave( projectile, 0, projectile, attackParams.pos, attackParams.dir, CreateFireWaveSegment )
    StopSoundOnEntity( projectile, "flamewall_flame_start" )

	projectile.Destroy()
}

// modified: stryder_fire_wave
bool function CreateFireWaveSegment( entity projectile, int projectileCount, entity inflictor, entity movingGeo, vector pos, vector angles, int waveCount )
{
	projectile.SetOrigin( pos )

	float damageScalar = 1.0
	// not using charged shot
	//bool isChargedShot = projectile.proj.isChargedShot
	//if ( isChargedShot )
	//	damageScalar = 1.5
	
	vector fxPos = pos + < 0, 0, 15 > // flamewall fx needs a bit offset
	entity fx = PlayFX( $"P_wpn_meteor_wall", fxPos, angles )
	EffectSetControlPointVector( fx, 1, pos ) // use current pos, or the fx will spread to map's< 0, 0, 0 >
	thread FireWaveFXLifeTime( fx )

	fxPos = pos - < 0, 0, 50 > // exp fx needs a bit offset
	PlayFX( $"impact_exp_burst_FRAG_2", fxPos, angles )

	int damageSourceID = eDamageSourceId.mp_titanweapon_flame_ring
	int pilotDamage = int( float( projectile.GetProjectileWeaponSettingInt( eWeaponVar.damage_near_value ) ) * damageScalar )
	int titanDamage = int( float( projectile.GetProjectileWeaponSettingInt( eWeaponVar.damage_near_value_titanarmor ) ) * damageScalar )

	RadiusDamage(
		pos,
		projectile.GetOwner(), 			//attacker
		inflictor, 						//inflictor
		pilotDamage,
		titanDamage,
		112, 							// inner radius
		112, 							// outer radius
		SF_ENVEXPLOSION_NO_DAMAGEOWNER | SF_ENVEXPLOSION_MASK_BRUSHONLY | SF_ENVEXPLOSION_NO_NPC_SOUND_EVENT,
		0, 								// distanceFromAttacker
		0, 								// explosionForce
		DF_EXPLOSION | DF_STOPS_TITAN_REGEN,
		damageSourceID
	)

	return true
}

void function FireWaveFXLifeTime( entity fx )
{
	fx.EndSignal( "OnDestroy" )
	wait 0.5 // enough time for the fx
	EffectStop( fx )
}

// npc
var function OnWeaponNpcPrimaryAttack_titanweapon_fire_wave( entity weapon, WeaponPrimaryAttackParams attackParams )
{
    const float FUSE_TIME = 99.0
	entity projectile = weapon.FireWeaponGrenade( attackParams.pos, attackParams.dir, < 0,0,0 >, FUSE_TIME, damageTypes.projectileImpact, damageTypes.explosive, false, true, true )
	if ( IsValid( projectile ) )
		thread BeginFireWave( projectile, attackParams )

	return 1
}
#endif