untyped

global function ClassWallrun_Init

global function Wallrun_AddPlayer
global function Wallrun_OnPlayerSpawn
global function Wallrun_OnPlayerDeath
global function Wallrun_PlayerTookDamage
global function Wallrun_EnforceWeaponDefaults
global function Wallrun_CreateCopyOfPilotModel

// nessie modify
global function Wallrun_SetProtectionForRodeoPlayerStyle
// vanilla hardcode turns to settings
global function Wallrun_AddRodeoProtectionIgnoredDamageSourceId
global function Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId

// modified callbacks here
// gets called on every pilot-related model creation( npc pilot eject, titan termination viewbody... etc )
global function Wallrun_AddCallback_OnCreatingPilotModel
global function Wallrun_RunCallbacks_OnCreatingPilotModel // to be shared with _melee_synced_titan.gnut and 

global enum eRodeoPlayerProtectionStyle
{
	// no damage protection against current titan rodeoing
	PROTECTION_AGAINST_NPC,				// vanilla behavior: protect player from all npcs
	PROTECTION_AGAINST_NPC_MELEE,		// protect player from accidental npc melee attacks
	PROTECTION_AGAINST_NON_AGGRO_NPC,	// protect player from npcs who're not aggro at them, and invalid attacker case
	PROTECTION_AGAINST_NON_PLAYER,		// protect player from all damages those are not done by other players
	PROTECTION_AGAINST_NON_TITAN_AND_NPC_MELEE,		// protect player from npc melee and non-titan explosion damages
}

// modified settings struct
struct
{
	int rodeoPlayerProtectionStyle = eRodeoPlayerProtectionStyle.PROTECTION_AGAINST_NPC
	// vanilla hardcode turns to settings
	array<int> rodeoProtectionIgnoredDamageSourceIds
	array<int> rodeoDamageAdjustmentIgnoredDamageSourceIds

	array<void functionref( entity pilotModel, entity owner )> onCreatingPilotModelCallbacks
} file

function ClassWallrun_Init()
{
	// Make weapons less effective when playing at higher difficulty.
	level.lethalityMods <- {}


	// vanilla hardcode turns to settings
	Wallrun_AddRodeoProtectionIgnoredDamageSourceId( eDamageSourceId.mp_titanability_smoke )
	Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId( eDamageSourceId.rodeo_trap )
	Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId( eDamageSourceId.mp_titanweapon_vortex_shield )
	Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId( eDamageSourceId.mp_titanweapon_vortex_shield_ion )
	Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId( eDamageSourceId.mp_titanability_smoke )
	Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId( eDamageSourceId.mp_weapon_satchel ) //added so that rodeoing players are no longer invulnerable to their satchels when detonated by Titan's smoke
}

function Wallrun_AddPlayer( player )
{
	player.playerClassData[level.pilotClass] <- {}
}


function Wallrun_EnforceWeaponDefaults( player )
{
	if ( player.playerClassData[ level.pilotClass ].primaryWeapon )
	{
		// settings already exist
		return
	}

	player.playerClassData[ level.pilotClass ].primaryWeapon = "mp_weapon_r97"
	player.playerClassData[ level.pilotClass ].secondaryWeapon = "mp_weapon_sniper"

	local offhandWeaponTable = {}
	offhandWeaponTable[0] <- { weapon = "mp_weapon_frag_grenade", mods = [] }
	offhandWeaponTable[1] <- { weapon = "mp_ability_heal", mods = [] }

	player.playerClassData[ level.pilotClass ].offhandWeapons = offhandWeaponTable
	player.playerClassData[ level.pilotClass ].playerSetFile = DEFAULT_PILOT_SETTINGS
}


function Wallrun_OnPlayerSpawn( player )
{
}


function Wallrun_PlayerTookDamage( entity player, damageInfo, entity attacker )
{
	if ( IsDemigod( player ) )
	{
		EntityDemigod_TryAdjustDamageInfo( player, damageInfo )
		return
	}

	AdjustDamageForRodeoPlayers( player, damageInfo, attacker )

	#if VERBOSE_DAMAGE_PRINTOUTS
		printt( "    After Wallrun_PlayerTookDamage:", DamageInfo_GetDamage( damageInfo ) )
	#endif

	//modified
	if ( GetShieldHealthMaxWithFix( player ) > 0 
		&& !( DamageInfo_GetCustomDamageType( damageInfo ) & DF_BYPASS_SHIELD ) )
	{
		local shieldDamage = PilotShieldHealthUpdate( player, damageInfo )
		return shieldDamage
	}

	return
}

function AdjustDamageForRodeoPlayers( entity player, var damageInfo, entity attacker )
{
	if ( player == attacker )
		return

	local titanSoulRodeoed = player.GetTitanSoulBeingRodeoed()
	if ( !IsValid( titanSoulRodeoed ) )
		return

	local playerParent = titanSoulRodeoed.GetTitan()

	// dont let npcs hurt rodeo player
	// nessie: adding a toggle. I'm adding various npcs to battle, needs to allow npcs damage rodeoing players
	//if ( attacker.IsNPC() && attacker != playerParent && DamageInfo_GetDamageSourceIdentifier( damageInfo ) != eDamageSourceId.mp_titanability_smoke )
	
	// default damage protection case: when getting attacked by npc
	bool removeDamage = IsValid( attacker ) && attacker.IsNPC() 			
	
	if ( file.rodeoPlayerProtectionStyle == eRodeoPlayerProtectionStyle.PROTECTION_AGAINST_NPC_MELEE )
	{
		// modified checks: npc melee protection( mostly from auto-titan and reaper )
		// they have extramely large radius that can hurt rodeo player when targeting titan
		// should remove their damage
		removeDamage = IsValid( attacker )
					   && attacker.IsNPC()
					   && attacker.GetEnemy() != player // to avoid meleeing rodeo player when targeting titan
					   && bool( DamageInfo_GetCustomDamageType( damageInfo ) & DF_MELEE )
	}
	else if ( file.rodeoPlayerProtectionStyle == eRodeoPlayerProtectionStyle.PROTECTION_AGAINST_NON_AGGRO_NPC )
	{
		// modified damage removal case: only take damage from players or npcs who're aggro at player
		removeDamage = ( !IsValid( attacker ) || !( attacker instanceof CBaseCombatCharacter ) )
					   || ( attacker.IsNPC() && attacker.GetEnemy() != player )
	}
	else if ( file.rodeoPlayerProtectionStyle == eRodeoPlayerProtectionStyle.PROTECTION_AGAINST_NON_PLAYER )
	{
		// modified damage removal case: only take damage from players
		removeDamage = !IsValid( attacker ) || !attacker.IsPlayer()
	}
	else if ( file.rodeoPlayerProtectionStyle == eRodeoPlayerProtectionStyle.PROTECTION_AGAINST_NON_TITAN_AND_NPC_MELEE )
	{
		// modified damage removal case: take full damage from titans, don't take damage from other npcs if they're not targeting player, don't take npc melee damage if they're not targeting player
		removeDamage = !IsValid( attacker ) 
					   || ( 
					   	   attacker.IsNPC() 
						   && ( 
						   	   ( !attacker.IsTitan() && attacker.GetEnemy() != player )
						   	   || ( attacker.GetEnemy() != player && bool( DamageInfo_GetCustomDamageType( damageInfo ) & DF_MELEE ) ) 
						   )
					   )
	}

	// HARDCODE: if it's parent themselves' damage or electric smoke damage, we never do protection
	if ( attacker == playerParent || file.rodeoProtectionIgnoredDamageSourceIds.contains( DamageInfo_GetDamageSourceIdentifier( damageInfo ) ) )
		removeDamage = false

	// debug
	//print( "removeDamage for rodeo player: " + string( removeDamage ) )
	if ( removeDamage )
	{
		DamageInfo_SetDamage( damageInfo, 0 )
		return
	}

	local damage = DamageInfo_GetDamage( damageInfo )

	if ( !ShouldAdjustDamageForRodeoPlayer( damageInfo ) )
		return

	local maxPer500ms

	if ( attacker == playerParent )
	{
		// rodeo'd player can't damage quite as much
		maxPer500ms = 56
	}
	else
	if ( playerParent.GetTeam() == player.GetTeam() )
	{
		// riding same team titan protects you a bit from random fire on that titan
		if ( DamageInfo_GetCustomDamageType( damageInfo ) & DF_EXPLOSION )
		{
			maxPer500ms = 75
		}
		else if ( DamageInfo_GetCustomDamageType( damageInfo ) & DF_MELEE  ) //If melee, players still die in one hit
		{
			maxPer500ms = player.GetMaxHealth() + 1
		}
		else
		{
			maxPer500ms = 175
		}
	}
	else
	{
		return
	}

	//Set a cap on how much damage the playerParent can do.
	local damageTaken = GetTotalDamageTakenInTime( player, 0.5 )

	local allowedDamage = maxPer500ms - damageTaken
	if ( damage < allowedDamage )
		return

	damage = allowedDamage
	if ( damage <= 0 )
		damage = 0

	DamageInfo_SetDamage( damageInfo, damage )
}


function ShouldAdjustDamageForRodeoPlayer( damageInfo )
{
	int sourceID = DamageInfo_GetDamageSourceIdentifier( damageInfo )

	// vanilla hardcode turns to settings
	/*
	switch( sourceID )
	{
		case eDamageSourceId.rodeo_trap:
		case eDamageSourceId.mp_titanweapon_vortex_shield:
		case eDamageSourceId.mp_titanweapon_vortex_shield_ion:
		case eDamageSourceId.mp_titanability_smoke:
		case eDamageSourceId.mp_weapon_satchel:	//added so that rodeoing players are no longer invulnerable to their satchels when detonated by Titan's smoke
			return false

		default:
			return true
	}
	*/
	return file.rodeoDamageAdjustmentIgnoredDamageSourceIds.contains( sourceID )
}


function Wallrun_OnPlayerDeath( entity player, damageInfo )
{
	if ( IsValidHeadShot( damageInfo, player ) )
	{
		int damageType = DamageInfo_GetCustomDamageType( damageInfo )
		local soundAlias
		if ( damageType & DF_SHOTGUN )
		{
			EmitSoundOnEntityOnlyToPlayer( player, player, "Flesh.Shotgun.BulletImpact_Headshot_3P_vs_1P" )
			soundAlias = "Flesh.Shotgun.BulletImpact_Headshot_3P_vs_3P"
		}
		else if ( damageType & damageTypes.bullet || damageType & DF_BULLET )
		{
			EmitSoundOnEntityOnlyToPlayer( player, player, "Flesh.Light.BulletImpact_Headshot_3P_vs_1P" )
			soundAlias = "Flesh.Light.BulletImpact_Headshot_3P_vs_3P"
		}
		else if ( damageType & damageTypes.largeCaliber ||  damageType & DF_GIB  )
		{
			EmitSoundOnEntityOnlyToPlayer( player, player, "Flesh.Heavy.BulletImpact_Headshot_3P_vs_1P" )
			soundAlias = "Flesh.Heavy.BulletImpact_Headshot_3P_vs_3P"
		}

		if ( soundAlias )
		{
			entity attacker = DamageInfo_GetAttacker( damageInfo )
			array<entity> pilotArray = GetPlayerArray()
			//Iterating because we need to not play this sound on 2 pilots and the function only allows for 1. Performance difference is negligible according to Eric M between this and adding a specific code function.
			foreach ( pilot in pilotArray )
			{
				if ( !IsValid( pilot ) )
					continue

				if ( pilot == player || pilot == attacker )
					continue

				EmitSoundOnEntityOnlyToPlayer( player, pilot, soundAlias )
			}
		}
	}
}


entity function Wallrun_CreateCopyOfPilotModel( entity player )
{
	const string PLAYER_SETTINGS_FIELD = "bodymodel"

	asset modelName
	if ( player.IsTitan() )
	{
		modelName = GetPlayerSettingsAssetForClassName( player.s.storedPlayerSettings, PLAYER_SETTINGS_FIELD )
	}
	else
	{
		modelName = player.GetPlayerSettingsAsset( PLAYER_SETTINGS_FIELD )
	}

	entity model = CreatePropDynamic( modelName )

	SetTeam( model, player.GetTeam() )

	//model.SetSkin( 0 )

	// nessie modify: add camo support
	// this sucks: prop-serverside or npc don't have correct camo, only prop-clientside can do with camo stuffs
	/*
	int camoIndex = player.GetCamo()
	// titan condition: get stored camo stuffs
	if ( player.IsTitan() )
		camoIndex = expect int( player.s.storedPlayerCamoIndex )

	if ( camoIndex > 0 )
	{
		model.SetSkin( PILOT_SKIN_INDEX_CAMO )
		model.SetCamo( camoIndex )
	}
	else
	{
		model.SetSkin( 0 )
		model.SetCamo( -1 )
	}
	*/
	//

	RandomizeHead( model )

	// modified here: run callbacks for every pilot model creation
	// Added via Wallrun_AddCallback_OnCreatingPilotModel
	Wallrun_RunCallbacks_OnCreatingPilotModel( model, player )

	return model
}


// nessie modify
void function Wallrun_SetProtectionForRodeoPlayerStyle( int style )
{
	file.rodeoPlayerProtectionStyle = style
}

void function Wallrun_AddRodeoProtectionIgnoredDamageSourceId( int damageSourceId )
{
	if ( !file.rodeoProtectionIgnoredDamageSourceIds.contains( damageSourceId ) )
		file.rodeoProtectionIgnoredDamageSourceIds.append( damageSourceId )
}

void function Wallrun_AddRodeoDamageAdjustmentIgnoredDamageSourceId( int damageSourceId )
{
	if ( !file.rodeoDamageAdjustmentIgnoredDamageSourceIds.contains( damageSourceId ) )
		file.rodeoDamageAdjustmentIgnoredDamageSourceIds.append( damageSourceId )
}

void function Wallrun_AddCallback_OnCreatingPilotModel( void functionref( entity pilotModel, entity owner ) callbackFunc )
{
	if ( !file.onCreatingPilotModelCallbacks.contains( callbackFunc ) )
		file.onCreatingPilotModelCallbacks.append( callbackFunc )
}

void function Wallrun_RunCallbacks_OnCreatingPilotModel( entity pilotModel, entity owner )
{
	foreach ( callbackFunc in file.onCreatingPilotModelCallbacks )
		callbackFunc( pilotModel, owner )
}