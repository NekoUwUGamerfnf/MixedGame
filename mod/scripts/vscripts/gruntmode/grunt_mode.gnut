untyped
global function Modded_Gamemode_GruntMode_Init

// npc counts
const int SQUADS_PER_TEAM = 5
const int SQUADS_PER_TEAM_MORE_PLAYERS = 3 // maybe do reduced squad if we have more players and reached higher level
const int REAPERS_PER_TEAM = 1 // was 2, reduced reaper spawn since we're using tick spawner reapers

const int MARVINS_PER_TEAM = 0
const int PROWLERS_PER_TEAM = 4
const int PILOTS_PER_TEAM = 3

const int TITANS_PER_TEAM = 0
const int GUNSHIPS_PER_TEAM = 0 // was 2, removed until I figure out how to make gunships attacking

const float AIRDROP_INTERNAL = 40.0 // fixed! ->// time for each air drops, current hardcoded( for airdrops now dissolves only after certain time )

// escalator
const int LEVEL_SPECTRES = 50
const int LEVEL_STALKERS = 100
const int LEVEL_REAPERS = 180
const int LEVEL_GUNSHIPS = 200
const int LEVEL_TITANS = 250

// scores
const int PLAYER_SCORE_AMOUNT = 3
const int PLAYER_SPECIALIST_SCORE_AMOUNT = 5
const int PILOT_SCORE_AMOUNT = 5
const int GUNSHIP_SCORE_AMOUNT = 5
const int MARVIN_SCORE_AMOUNT = 1
const int GRUNT_SCORE_AMOUNT = 1
const int SPECTRE_SCORE_AMOUNT = 2
const int STALKER_SCORE_AMOUNT = 2
const int PROWLER_SCORE_AMOUNT = 2
const int REAPER_SCORE_AMOUNT = 5
const int DRONE_SCORE_AMOUNT = 0
const int TITAN_SCORE_AMOUNT = 10

const array<string> GRUNT_MODE_PLAYLIST =
[
	"mp_glitch",
    "mp_colony02",
    "mp_wargames",
    "mp_eden",
    "mp_drydock",
    "mp_black_water_canal",
    "mp_thaw",
    "mp_grave",
    "mp_homestead",
    "mp_forwardbase_kodai",
    "mp_angel_city",
    "mp_relic02",
    "mp_rise",
    "mp_complex3",
    "mp_crashsite3"
]

struct
{
	// Due to team based escalation everything is an array
	array< int > levels = [ LEVEL_SPECTRES, LEVEL_SPECTRES ]
	array< array< string > > podEntities = [ [ "npc_soldier" ], [ "npc_soldier" ] ]
	array< bool > reapers = [ false, false ]

	array< bool > marvins = [ false, false ]
	array< bool > prowlers = [ false, false ] // bind with stalkers
	array< bool > weapondrops = [ false, false ]

	array< bool > gunships = [ false, false ]
	array< bool > pilots = [ false, false ]
	array< bool > titans = [ false, false ]
} file


void function Modded_Gamemode_GruntMode_Init()
{
	BecomesGruntMode_Init() // init everything

	SetSpawnpointGamemodeOverride( ATTRITION ) // use bounty hunt spawns as vanilla game has no spawns explicitly defined for aitdm

	AddCallback_GameStateEnter( eGameState.Prematch, OnPrematchStart )
	AddCallback_GameStateEnter( eGameState.Playing, OnPlaying )
	AddCallback_GameStateEnter( eGameState.Postmatch, OnGamePostmatch )

	// scoring
	AddDeathCallback( "player", ForceAddScoreForPlayerOrNPCKill )

	// npc killed score event, using death callbacks for npc killing each other!
	// these two can't be handle by scoreEvents
	AddDeathCallback( "npc_pilot_elite", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_gunship", ForceAddScoreForPlayerOrNPCKill )

	AddDeathCallback( "npc_marvin", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_soldier", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_spectre", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_stalker", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_prowler", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_super_spectre", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_drone", ForceAddScoreForPlayerOrNPCKill )
	AddDeathCallback( "npc_titan", ForceAddScoreForPlayerOrNPCKill )

	AddCallback_OnClientConnected( OnPlayerConnected )

	AddCallback_NPCLeeched( OnSpectreLeeched )

	ScoreEvent_SetupEarnMeterValuesForMixedModes()

	ClassicMP_ForceDisableEpilogue( true )
	Riff_ForceBoostAvailability( eBoostAvailability.Disabled )
	Riff_ForceTitanAvailability( eTitanAvailability.Never )

	// so you're likely no need to wait so freaking long, !!! tempfix specific
	SetWaitingForPlayersMaxDuration( 1.0 )
    ClassicMP_SetCustomIntro( ClassicMP_GruntModeIntro_Setup, GRUNTMODE_INTRO_LENGTH )
}

//------------------------------------------------------

void function OnPrematchStart()
{
	thread StratonHornetDogfightsIntense()
}

void function OnPlaying()
{
	// don't run spawning code if ains and nms aren't up to date
	if ( GetAINScriptVersion() == AIN_REV && GetNodeCount() != 0 )
	{
		thread SpawnIntroBatch( TEAM_MILITIA )
		thread SpawnIntroBatch( TEAM_IMC )
	}

	// cleaning up
	CleanUpOverloadedTicks()
	TryCleanupBoredNPCGameLong()
}

void function OnGamePostmatch()
{
	array<string> curPlayList = GRUNT_MODE_PLAYLIST
    int mapIndex = curPlayList.find( GetMapName() )
    mapIndex += 1 // next map index
	if( mapIndex == curPlayList.len() ) // reached last map?
		mapIndex = 0
	string nextMap = curPlayList[mapIndex]

    ServerCommand( "map " + nextMap )
}

void function OnPlayerConnected( entity player )
{
	Remote_CallFunction_NonReplay( player, "ServerCallback_AITDM_OnPlayerConnected" )
	Chat_ServerPrivateMessage( player, "娱乐性质服务器，请多多包容其余玩家。复活会以随机的兵种重生，虚假的步枪兵战争。由于npc数目过多，卡顿属正常现象", false )
}

//------------------------------------------------------

void function ForceAddScoreForPlayerOrNPCKill( entity victim, var damageInfo )
{
	entity attacker = DamageInfo_GetAttacker( damageInfo )
	if( !IsValid( attacker ) )
		return
	if( !attacker.IsNPC() && !attacker.IsPlayer() ) // not killed by anybody
		return

	if ( attacker == victim ) // suicide!
		return

	// if it's a npc with bossplayer/owner...
	if ( attacker.IsNPC() )
	{
		// reassign attacker
		if ( IsValid( attacker.GetBossPlayer() ) )
			attacker = attacker.GetBossPlayer()
		if ( IsValid( attacker.GetOwner() ) )
			attacker = attacker.GetOwner()
	}

	if ( victim.IsNPC() )
	{
		// if victim is a npc that owned by players, don't add score
		entity bossPlayer = victim.GetBossPlayer()
		entity owner = victim.GetOwner()
		if ( IsValid( bossPlayer ) )
		{
			if ( bossPlayer.IsPlayer() )
				return
		}
		if ( IsValid( owner ) )
		{
			if ( owner.IsPlayer() )
				return
		}
	}

	string className = victim.GetClassName()
	int scoreToAdd = 0
	string scoreEvent = "" // for npcs can't handle by OnNPCKilled(), do a extra score event

	if ( victim.IsPlayer() )
	{
		// consider these are specialist player grunts 
		if( GruntMode_IsSpecialistClass( victim ) )
			scoreToAdd = PLAYER_SPECIALIST_SCORE_AMOUNT
		else
			scoreToAdd = PLAYER_SCORE_AMOUNT
	}

	if ( className == "npc_pilot_elite" )
	{
		scoreToAdd = PILOT_SCORE_AMOUNT
		scoreEvent = "EliminatePilot"
	}

	if ( className == "npc_gunship" )
		scoreToAdd = GUNSHIP_SCORE_AMOUNT

	if ( className == "npc_marvin" )
		scoreToAdd = MARVIN_SCORE_AMOUNT

	if ( className == "npc_prowler" )
		scoreToAdd = PROWLER_SCORE_AMOUNT

	if ( className == "npc_spectre" )
		scoreToAdd = SPECTRE_SCORE_AMOUNT

	if ( className == "npc_stalker" )
		scoreToAdd = STALKER_SCORE_AMOUNT

	if ( className == "npc_super_spectre" )
		scoreToAdd = REAPER_SCORE_AMOUNT

	if ( className == "npc_soldier" )
		scoreToAdd = GRUNT_SCORE_AMOUNT
	
	if ( className == "npc_drone" )
		scoreToAdd = DRONE_SCORE_AMOUNT

	// Player ejecting triggers this without the extra check
	if ( victim.IsTitan() && victim.GetBossPlayer() != attacker )
	{
		if ( TitanHasNpcPilot( victim ) ) // pilot was killed without ejecting
		{
			scoreToAdd = TITAN_SCORE_AMOUNT + PILOT_SCORE_AMOUNT
			scoreEvent = "EliminatePilot"
		}
		else // ejected or auto titan
			scoreToAdd = TITAN_SCORE_AMOUNT
	}

	if ( scoreToAdd > 0 )
		AddTeamScore( attacker.GetTeam(), scoreToAdd ) // this can handle both players' and npcs' kills
	if( attacker.IsPlayer() ) // specific for players
	{
		if ( scoreEvent != "" )
			AddPlayerScore( attacker, scoreEvent, victim )
		if ( scoreToAdd > 0 )
		{
			attacker.AddToPlayerGameStat( PGS_ASSAULT_SCORE, scoreToAdd )
			attacker.SetPlayerNetInt( "AT_bonusPoints", attacker.GetPlayerGameStat( PGS_ASSAULT_SCORE ) )
		}
	}
}

//------------------------------------------------------

void function SpawnIntroBatch( int team )
{
	// intro spawn
	for ( int i = 0; i < SQUADS_PER_TEAM; i++ )
	{
		if ( RandomInt( 4 ) > 0 ) // 75% chance of spawning a specialist squad(??
		{
			array< entity > points = SpawnPoints_GetDropPod()
			entity node = points[ GetSpawnPointIndex( points, team ) ]
			// 25% chance of spawning a shield captain
			string leaderSet = RandomInt( 4 ) == 0 ? "npc_soldier_shield_captain" : "npc_soldier_pve_specialist"
			//print( "RUNNING ExtraSpawner_SpawnSpecialistGruntDropPod()" )
			thread ExtraSpawner_SpawnSpecialistGruntDropPod( node.GetOrigin(), node.GetAngles(), team, "npc_soldier", leaderSet, 250, SquadHandler )
			wait 5 // wait 5s per spawn, or player will have model issue since there're too many entities
		}
		else
		{
			array< entity > points = GetZiplineDropshipSpawns()

			// Prefer dropship when spawning grunts
			if ( points.len() / 4 > 0 ) // must have more than 4 points
			{
				entity node = points[ GetSpawnPointIndex( points, team ) ]
				thread ExtraSpawner_SpawnDropShip( node.GetOrigin(), node.GetAngles(), team, 4, SquadHandler )
				wait 10
				continue
			}

			points = SpawnPoints_GetDropPod()
			entity node = points[ GetSpawnPointIndex( points, team ) ]
			thread ExtraSpawner_SpawnDropPod( node.GetOrigin(), node.GetAngles(), team, "npc_soldier", SquadHandler )
		}
	}


	thread Spawner( team )
	thread SpawnerExtend( team )
	thread SpawnerWeapons( team )
}

// Populates the match
void function Spawner( int team )
{
	svGlobal.levelEnt.EndSignal( "GameStateChanged" )

	int index = team == TEAM_MILITIA ? 0 : 1

	while( true )
	{
		if( GetGameState() == eGameState.Playing )
		{
			Escalate( team )

			// TODO: this should possibly not count scripted npc spawns, probably only the ones spawned by this script
			array<entity> npcs = GetNPCArrayOfTeam( team )
			int count = npcs.len()
			int reaperCount = GetNPCArrayEx( "npc_super_spectre", team, -1, <0,0,0>, -1 ).len()

			// REAPERS
			if ( file.reapers[ index ] )
			{
				array< entity > points = SpawnPoints_GetDropPod()
				if ( reaperCount < REAPERS_PER_TEAM )
				{
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					//waitthread AiGameModes_SpawnReaper( node.GetOrigin(), node.GetAngles(), team, "npc_super_spectre_aitdm", ReaperHandler )
					// crazy reaper
					waitthread ExtraSpawner_SpawnReaperCanLaunchTicks( node.GetOrigin(), node.GetAngles(), team )
				}
			}

			// NORMAL SPAWNS
			int maxSquads = SQUADS_PER_TEAM
			if ( ReachedPlayerCheckAmount() && file.reapers[ index ] ) // if we have pretty much players and reached reaper level
				maxSquads = SQUADS_PER_TEAM_MORE_PLAYERS // do reduced minions count
			if ( count < maxSquads * 4 - 2 )
			{
				if ( RandomInt( 3 ) > 0 ) // 66% chance of spawning a specialist squad(??
				{
					array< entity > points = SpawnPoints_GetDropPod()
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					string ent = file.podEntities[ index ][ RandomInt( file.podEntities[ index ].len() ) ]
					// 25% chance of spawning a shield captain
					string leaderSet = RandomInt( 4 ) == 0 ? "npc_soldier_shield_captain" : "npc_soldier_pve_specialist"
					// add health depending on what npc it spawning with
					int leaderHealth = 200 + ( 60 * file.podEntities[ index ].find( ent ) )
					//print( "RUNNING ExtraSpawner_SpawnSpecialistGruntDropPod()" )
					//print( "leaderHealth is: " + string( leaderHealth ) )
					thread ExtraSpawner_SpawnSpecialistGruntDropPod( node.GetOrigin(), node.GetAngles(), team, ent, leaderSet, leaderHealth, SquadHandler )
				}
				else
				{
					string ent = file.podEntities[ index ][ RandomInt( file.podEntities[ index ].len() ) ]

					array< entity > points = GetZiplineDropshipSpawns()

					// Prefer dropship when spawning grunts
					if ( ent == "npc_soldier" && points.len() / 4 > 0 ) // must have more than 4 points
					{
						entity node = points[ GetSpawnPointIndex( points, team ) ]
						waitthread ExtraSpawner_SpawnDropShip( node.GetOrigin(), node.GetAngles(), team, 4, SquadHandler )
						continue
					}

					points = SpawnPoints_GetDropPod()
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					thread ExtraSpawner_SpawnDropPod( node.GetOrigin(), node.GetAngles(), team, ent, SquadHandler )
				}
			}
		}
		else
			break
		WaitFrame()
	}
}

void function SpawnerExtend( int team )
{
	svGlobal.levelEnt.EndSignal( "GameStateChanged" )

	int index = team == TEAM_MILITIA ? 0 : 1

	while( true )
	{
		if( GetGameState() == eGameState.Playing )
		{
			Escalate( team )

			int marvinCount = GetNPCArrayEx( "npc_marvin", team, -1, <0,0,0>, -1 ).len()
			int prowlerCount = GetNPCArrayEx( "npc_prowler", team, -1, <0,0,0>, -1 ).len()
			int gunshipCount = GetNPCArrayEx( "npc_gunship", team, -1, <0,0,0>, -1 ).len()
	        int titanCount = GetNPCArrayEx( "npc_titan", team, -1, <0,0,0>, -1 ).len()
	        int pilotCount = GetNPCArrayEx( "npc_pilot_elite", team, -1, <0,0,0>, -1 ).len() + GetNPCArrayEx( "npc_titan", team, -1, <0,0,0>, -1 ).len()


	        // GUNSHIPS
	        if ( file.gunships[ index ] )
			{
				array< entity > points = SpawnPoints_GetDropPod()
				if ( gunshipCount < GUNSHIPS_PER_TEAM )
				{
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					waitthread ExtraSpawner_SpawnGunShip( node.GetOrigin(), node.GetAngles(), team)
				}
			}

			// TITANS
			if ( file.titans[ index ] )
			{
				array< entity > points = SpawnPoints_GetDropPod()
				if ( titanCount < TITANS_PER_TEAM )
				{
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					waitthread ExtraSpawner_SpawnTitanRandom( node.GetOrigin(), node.GetAngles(), team, true, TitanHandler )
				}
			}

			// PILOTS
			if ( file.pilots[ index ] )
			{
				array< entity > points
				points.extend( SpawnPoints_GetTitanStart( TEAM_IMC ) )
				points.extend( SpawnPoints_GetTitanStart( TEAM_MILITIA ) )
				if ( pilotCount < PILOTS_PER_TEAM )
				{
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					//entity titan = AiGameModes_SpawnTitanRandom( node.GetOrigin(), node.GetAngles(), team, TitanHandler )
					if ( RandomInt( 3 ) == 0 ) // boss titans has 33% chance to spawn
					{
						switch ( RandomInt( 4 ) )
						{
							case 0:
								waitthread ExtraSpawner_SpawnBTWithCooper( node.GetOrigin(), node.GetAngles(), team )
								break
							case 1:
								waitthread ExtraSpawner_SpawnRoninWithAsh( node.GetOrigin(), node.GetAngles(), team )
								break
							case 2:
								waitthread ExtraSpawner_SpawnLegionWithBlisk( node.GetOrigin(), node.GetAngles(), team )
								break
							case 3:
								waitthread ExtraSpawner_SpawnMonarchWithSarah( node.GetOrigin(), node.GetAngles(), team )
								break
						}
					}
					else
						waitthread ExtraSpawner_SpawnPilotCanEmbark( node.GetOrigin(), node.GetAngles(), team )
				}
			}

			// MARVINS
			if ( file.marvins[ index ] )
			{
				string ent = "npc_marvin"
				array< entity > points = SpawnPoints_GetDropPod()
				if ( marvinCount < MARVINS_PER_TEAM )
				{
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					//waitthread AiGameModes_SpawnDropPod( node.GetOrigin(), node.GetAngles(), team, ent )
					thread ExtraSpawner_SpawnNPCInDropPod( node.GetOrigin(), node.GetAngles(), team, ent )
					
				}
			}

			// PROWLERS
			if ( file.prowlers[ index ] )
			{
				string ent = "npc_prowler"
				array< entity > points = SpawnPoints_GetDropPod()
				if ( prowlerCount < PROWLERS_PER_TEAM )
				{
					entity node = points[ GetSpawnPointIndex( points, team ) ]
					//waitthread AiGameModes_SpawnDropPod( node.GetOrigin(), node.GetAngles(), team, ent )
					thread ExtraSpawner_SpawnNPCInDropPod( node.GetOrigin(), node.GetAngles(), team, ent, NPCHandler )
					
				}
			}
		}
		else
			break
		WaitFrame()
	}
}

void function SpawnerWeapons( int team )
{
	//svGlobal.levelEnt.EndSignal( "GameStateChanged" )

	while( true )
	{
		wait AIRDROP_INTERNAL
		if( GetGameState() == eGameState.Playing )
		{
			foreach( entity player in GetPlayerArrayOfTeam( team ) )
			{
				NSSendLargeMessageToPlayer( player, "補給艙運送中! ", "補充重火力與武器彈藥，並獲取一把榴彈兵武器", 15, "rui/callsigns/callsign_69_col" )
				//NSSendLargeMessageToPlayer( player, "Delivering Care Package!", "Refill ordnance and weapon ammo, also get a grenadier weapon", 15, "rui/callsigns/callsign_69_col" )
			}
			/* // older version
			foreach( entity player in GetPlayerArray() )
			{
				if( IsValid(player) )
					SendHudMessage(player, "Delivering Care Package",  -1, 0.3, 255, 255, 0, 255, 0.15, 3, 1)
			}
			*/
			array< entity > points = SpawnPoints_GetDropPod()

			entity node = points[ GetSpawnPointIndex( points, team ) ]
			//waitthread SpawnReaperDorpsWeapons( node.GetOrigin(), node.GetAngles(), WEAPONS, MODS )
			ExtraSpawner_SpawnCarePackageToGetWeapons( node.GetOrigin(), node.GetAngles(), AIRDROP_INTERNAL - 10 ) // carepackages will take 10s to drop
		}
		else
			break
	}
}

// Based on points tries to balance match
void function Escalate( int team )
{
	int score = GameRules_GetTeamScore( team )
	int index = team == TEAM_MILITIA ? 1 : 0
	// This does the "Enemy x incoming" text
	string defcon = team == TEAM_MILITIA ? "IMCdefcon" : "MILdefcon"

	if ( score < file.levels[ index ] )
		return

	switch ( file.levels[ index ] )
	{
		case LEVEL_SPECTRES:
			file.levels[ index ] = LEVEL_STALKERS
			file.marvins[ index ] = true
			file.podEntities[ index ].append( "npc_spectre" )
			SetGlobalNetInt( defcon, 2 )
			return

		case LEVEL_STALKERS:
			file.levels[ index ] = LEVEL_REAPERS
			file.marvins[ index ] = false
			file.weapondrops[ index ] = true
			file.prowlers[ index ] = true
			file.podEntities[ index ].append( "npc_stalker" )
			SetGlobalNetInt( defcon, 3 )
			return

		case LEVEL_REAPERS:
			file.levels[ index ] = LEVEL_GUNSHIPS
			file.reapers[ index ] = true
			SetGlobalNetInt( defcon, 4 )
			return

        case LEVEL_GUNSHIPS:
			file.levels[ index ] = LEVEL_TITANS
			file.gunships[ index ] = true
			SetGlobalNetInt( defcon, 5 )
			return


		case LEVEL_TITANS:
			file.levels[ index ] = 9999
			file.prowlers[ index ] = false
			file.pilots[ index ] = true
			file.titans[ index ] = true
			SetGlobalNetInt( defcon, 6 )
			return
	}

	unreachable // hopefully
}

//------------------------------------------------------

int function GetSpawnPointIndex( array< entity > points, int team )
{
	entity zone = DecideSpawnZone_Generic( points, team )

	if ( IsValid( zone ) )
	{
		// 20 Tries to get a random point close to the zone
		for ( int i = 0; i < 20; i++ )
		{
			int index = RandomInt( points.len() )

			if ( Distance2D( points[ index ].GetOrigin(), zone.GetOrigin() ) < 6000 )
				return index
		}
	}

	return RandomInt( points.len() )
}

bool function ReachedPlayerCheckAmount()
{
	int maxPlayers = GetCurrentPlaylistVarInt( "max_players", 16 )
	if ( float ( GetPlayerArray().len() ) >= float ( maxPlayers ) * 0.6 )
		return true
	return false
}

//------------------------------------------------------

// tells infantry where to go
// In vanilla there seem to be preset paths ai follow to get to the other teams vone and capture it
// AI can also flee deeper into their zone suggesting someone spent way too much time on this
void function SquadHandler( array<entity> guys )
{
	int team = guys[0].GetTeam()
	// Not all maps have assaultpoints / have weird assault points ( looking at you ac )
	// So we use enemies with a large radius
	while ( GetNPCArrayOfEnemies( team ).len() == 0 ) // if we can't find any enemy npcs, keep waiting
		WaitFrame()

	// our waiting is end, check if any soldiers left
	bool squadAlive = false
	foreach ( entity guy in guys )
	{
		if ( IsAlive( guy ) )
			squadAlive = true
	}
	if ( !squadAlive )
		return

	array<entity> points = GetNPCArrayOfEnemies( team )
	
	vector point
	point = points[ RandomInt( points.len() ) ].GetOrigin()
	
	array<entity> players = GetPlayerArrayOfEnemies( team )
	
	// Setup AI
	foreach ( guy in guys )
	{
		// Check if alive, since we've delayed this
		if ( !IsAlive( guy ) )
		{
			guys.removebyvalue( guy )
			continue
		}
		guy.EnableNPCFlag( NPC_ALLOW_PATROL | NPC_ALLOW_INVESTIGATE | NPC_ALLOW_HAND_SIGNALS | NPC_ALLOW_FLEE )
		guy.AssaultPoint( point )
		guy.AssaultSetGoalRadius( 1600 ) // 1600 is minimum for npc_stalker, works fine for others
		
		// show on enemy radar
		foreach ( player in players )
			guy.Minimap_AlwaysShow( 0, player )
	}
	
	// Every 5 - 15 secs change AssaultPoint
	while ( true )
	{	
		foreach ( guy in guys )
		{
			// Check if alive
			if ( !IsAlive( guy ) )
			{
				guys.removebyvalue( guy )
				continue
			}
			// Stop func if our squad has been killed off
			if ( guys.len() == 0 )
				return
		}

		// Get point and send our whole squad to it
		points = []
		points.extend( GetNPCArrayOfEnemies( team ) )
		points.extend( GetPlayerArrayOfEnemies_Alive( team ) ) // also find players since they're also grunts
		if ( points.len() == 0 ) // can't find any points here
		{
			WaitFrame()
			continue
		}
		point = points[ RandomInt( points.len() ) ].GetOrigin()
		
		foreach ( guy in guys )
		{
			if ( IsAlive( guy ) )
				guy.AssaultPoint( point )
		}
		wait RandomFloatRange(5.0,15.0)
	}
}

// handle a single npc
void function NPCHandler( entity npc )
{
	SquadHandler( [npc] )
}

// handle titans
void function TitanHandler( entity titan )
{
	titan.EndSignal( "OnDestroy" )
	titan.EndSignal( "OnDeath" )
	
	int team = titan.GetTeam()
	while ( GetNPCArrayOfEnemies( team ).len() == 0 )
		WaitFrame()

	array<entity> points = GetNPCArrayOfEnemies( team )
	
	vector point
	point = points[ RandomInt( points.len() ) ].GetOrigin()
	
	array<entity> players = GetPlayerArrayOfEnemies( team )
	
	// Setup AI
	titan.EnableNPCFlag( NPC_ALLOW_PATROL | NPC_ALLOW_INVESTIGATE | NPC_ALLOW_HAND_SIGNALS | NPC_ALLOW_FLEE )
	titan.AssaultPoint( point )
	titan.AssaultSetGoalRadius( 1600 ) // 1600 is minimum for npc_stalker, works fine for others
	
	// show on enemy radar
	foreach ( player in players )
		titan.Minimap_AlwaysShow( 0, player )
	
	// Every 5 - 15 secs change AssaultPoint
	while ( true )
	{	
		// Check if alive
		if ( !IsAlive( titan ) )
			return
		
		// Get point and send guy to it
		points = GetNPCArrayOfEnemies( titan.GetTeam() )
		if ( points.len() == 0 )
			continue
			
		point = points[ RandomInt( points.len() ) ].GetOrigin()
		
		titan.AssaultPoint( point )
		wait RandomFloatRange(5.0,15.0)
	}
}

// Award for hacking
void function OnSpectreLeeched( entity spectre, entity player )
{
	// Set Owner so we can filter in HandleScore
	spectre.SetOwner( player )
	// Add score + update network int to trigger the "Score +n" popup
	AddTeamScore( player.GetTeam(), 1 )
	player.AddToPlayerGameStat( PGS_ASSAULT_SCORE, 1 )
	player.SetPlayerNetInt("AT_bonusPoints", player.GetPlayerGameStat( PGS_ASSAULT_SCORE ) )
}

void function ReaperHandler( entity reaper )
{
	array<entity> players = GetPlayerArrayOfEnemies( reaper.GetTeam() )
	foreach ( player in players )
		reaper.Minimap_AlwaysShow( 0, player )
}

// ticks clean up
const int GRUNT_MAX_TICKS = 10 // prevent too much ticks stucking the server
void function CleanUpOverloadedTicks()
{
	thread CleanUpOverloadedTicks_Threaded()
}

void function CleanUpOverloadedTicks_Threaded()
{
	array<entity> tickArray = GetNPCArrayByClass( "npc_frag_drone" )
	if( tickArray.len() > GRUNT_MAX_TICKS ) // clean up ticks spawned by reapers
	{
		if( IsValid( tickArray[0] ) )
			tickArray[0].Destroy()
	}
}

// npc clean up
void function TryCleanupBoredNPCGameLong()
{
	thread TryCleanupBoredNPCGameLong_Threaded()
}

void function TryCleanupBoredNPCGameLong_Threaded()
{
	// keep searching new npcs and run CleanupBoredNPCThread() for them
	while ( true )
	{
		foreach ( entity npc in GetNPCArray() )
		{
			if ( !( "waitingToBeCleanUp" in npc.s ) )
				npc.s.waitingToBeCleanUp <- false
			if ( !npc.s.waitingToBeCleanUp )
			{
				thread CleanupBoredNPCThread( npc )
				npc.s.waitingToBeCleanUp = true
			}
		}
		WaitFrame()
	}
}

void function CleanupBoredNPCThread( entity guy )
{
	// track all ai that we spawn, ensure that they're never "bored" (i.e. stuck by themselves doing fuckall with nobody to see them) for too long
	// if they are, kill them so we can free up slots for more ai to spawn
	// we shouldn't ever kill ai if players would notice them die

	// NOTE: this partially covers up for the fact that we script ai alot less than vanilla probably does
	// vanilla probably messes more with making ai assaultpoint to fights when inactive and stuff like that, we don't do this so much

	if ( guy.GetClassName() == "npc_marvin" ) // never clean up marvins
		return
	guy.EndSignal( "OnDestroy" )
	// cover spawning time from dropship/pod + before we start cleaning up
	WaitFrame()
	while ( IsValid( guy.GetParent() ) )
		WaitFrame()

	wait 10.0 // initial wait
	int team = guy.GetTeam()
	vector lastCheckOrigin = guy.GetOrigin() // if they get stucked we also clean up
	int cleanupFailures = 0 // when this hits 2, cleanup the npc
	while ( cleanupFailures < 2 )
	{
		wait 10.0

		if ( guy.Anim_IsActive() ) // anim active, such as marvin doing a job or npc being executed
			continue

		// if the npc is owner by player, we skip checks
		entity owner = guy.GetOwner()
		entity bossPlayer = guy.GetBossPlayer()
		if ( IsValid( owner ) )
		{
			if ( owner.IsPlayer() )
				continue
		}
		if ( IsValid( bossPlayer ) )
		{
			if ( bossPlayer.IsPlayer() )
				continue
		}

		// if npc is a titan with bubbleshield, we skip checks
		if ( guy.IsTitan() )
		{
			entity soul = guy.GetTitanSoul()
			if( IsValid( soul ) )
			{
				entity bubbleShield = soul.soul.bubbleShield
				if ( IsValid( bubbleShield ) )
					continue
			}
		}

		array<entity> otherGuys = GetPlayerArray()
		otherGuys.extend( GetNPCArrayOfEnemies( team ) )

		bool failedChecks = false

		foreach ( entity otherGuy in otherGuys )
		{
			// skip dead people
			if ( !IsAlive( otherGuy ) )
				continue

			failedChecks = false

			// don't kill if too close to anything
			if ( Distance( otherGuy.GetOrigin(), guy.GetOrigin() ) < 2000.0 )
				break

			// don't kill if ai or players can see them
			if ( otherGuy.IsPlayer() )
			{
				if ( PlayerCanSee( otherGuy, guy, true, 135 ) )
					break
			}
			else
			{
				if ( otherGuy.CanSee( guy ) )
					break
			}

			// don't kill if they can see any ai
			if ( guy.CanSee( otherGuy ) )
				break

			failedChecks = true
		}

		if ( Distance( guy.GetOrigin(), lastCheckOrigin ) <= 64 ) // assume they're not moving
			failedChecks = true // mark as checks failed

		if ( failedChecks )
			cleanupFailures++
		else
			cleanupFailures--

		lastCheckOrigin = guy.GetOrigin()
	}

	print( "cleaning up bored npc: " + guy + " from team " + guy.GetTeam() )
	HideName( guy ) // prevent their title or health bar from showing
	TakeAllWeapons( guy ) // so they won't have a weapon floating
	guy.NotSolid()
	guy.Dissolve( ENTITY_DISSOLVE_CORE, Vector( 0, 0, 0 ), 500 ) // dissolve them to have better visual
	// titan being dissolved
	if ( guy.IsTitan() )
	{
		entity soul = guy.GetTitanSoul()
		if( IsValid( soul ) )
		{
			// end any rodeo on it
			ForceTitanRodeoToEnd( guy )
			// try to hide a cleaning up titan's batteryContainer
			entity batteryContainer = soul.soul.batteryContainer
			if ( IsValid( batteryContainer ) )
				batteryContainer.Hide()
		}
		
	}
}