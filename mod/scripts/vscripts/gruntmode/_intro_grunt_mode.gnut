untyped
global function ClassicMP_GruntModeIntro_Setup

const int MAX_DROPSHIP_PLAYERS = 4


global const float GRUNTMODE_INTRO_LENGTH = 6.0 // 10s shorter than normal intro
const float SPAWN_IN_DROPPOD_TIME = 1.0 // taken from sequence
// "GRUNTMODE_INTRO_LENGTH - SPAWN_IN_DROPPOD_TIME" better be lower than droppod's impact time(about 4s)

struct IntroDropship
{
	entity dropship

	int playersInDropship
	entity[MAX_DROPSHIP_PLAYERS] players
}

struct DropShipSequenceDate
{
	// idle anim
	string idleAnim
	string idlePOVAnim
	string idleAttachment = "ORIGIN"
	bool idleTeleport = true
	bool idleHideProxy = true
	vector idleAngles = < 0, 0, 0 >
	void functionref( entity ) idleViewConeFunc

	// jump anim
	string jumpAnim
	string jumpPOVAnim
	string jumpAttachment = "ORIGIN"
	bool jumpTeleport = false
	bool jumpHideProxy = true
	void functionref( entity ) jumpViewConeFunc
	string firstPersonJumpOutSound // Unfortunately first person anims play for everybody, not just the person who the anim is played upon, so script needs to play the sound as opposed to a qc entry
}

struct 
{
	array<entity> validDropshipSpawns
	array<DropShipSequenceDate> dropShipSequenceDateList

	// these used to be IntroDropship[2]s but i wanted to be able to use array.getrandom so they have to be actual arrays
	array<IntroDropship> militiaDropships
	array<IntroDropship> imcDropships
	
	// for respawning from droppod
	table<entity, bool> playerSpawnFromDropship

	float introStartTime
} file


void function ClassicMP_GruntModeIntro_Setup()
{
	InitValidDropshipSpawns()
	InitIntroDropshipSequenceData()
	AddCallback_OnClientConnected( DropshipIntro_OnClientConnected )	
	AddCallback_GameStateEnter( eGameState.Prematch, OnPrematchStart )
	AddCallback_GameStateEnter( eGameState.Playing, OnGameStart )
}

void function InitValidDropshipSpawns()
{
	array<entity> dropshipSpawns = GetEntArrayByClass_Expensive( "info_spawnpoint_dropship_start" )
	foreach ( entity dropshipSpawn in dropshipSpawns )
	{
		if ( GameModeRemove( dropshipSpawn ) ) // this is hardcoded, should modify it
			continue
		/* // should never, never use GetSpawnpointGamemodeOverride() on dropship spawns
		if ( dropshipSpawn.HasKey( "gamemode_" + GetSpawnpointGamemodeOverride() ) )
		{
			if ( dropshipSpawn.kv[ "gamemode_" + GetSpawnpointGamemodeOverride() ] == "0" )
				continue
		}
		*/
		
		file.validDropshipSpawns.append( dropshipSpawn )
	}

	if ( file.validDropshipSpawns.len() == 4 ) // only setup pickLoadout if there do have proper dropship spawns
	{
		SetPickLoadoutEnabled( true ) // required to make client play warpjump sound
		SetPickLoadoutDuration( 4.0 ) // enough time for playing the sound, don't want to make player wait too long
	}
}

void function InitIntroDropshipSequenceData()
{
    DropShipSequenceDate dataForPlayerA
    dataForPlayerA.idleAnim           		= "Classic_MP_flyin_exit_playerA_idle"
    dataForPlayerA.idlePOVAnim        		= "Classic_MP_flyin_exit_povA_idle"
	dataForPlayerA.idleAttachment			= "ORIGIN"
	dataForPlayerA.idleTeleport 			= true
	dataForPlayerA.idleHideProxy 			= true
    dataForPlayerA.idleAngles           	= < 0, -18.0, 0 >
	dataForPlayerA.idleViewConeFunc       	= ViewConeRampFree

	dataForPlayerA.jumpAnim           		= "Classic_MP_flyin_exit_playerA_jump"
    dataForPlayerA.jumpPOVAnim        		= "Classic_MP_flyin_exit_povA_jump"
	dataForPlayerA.jumpAttachment			= "ORIGIN"
	dataForPlayerA.jumpTeleport 			= false
	dataForPlayerA.jumpHideProxy 			= true
	dataForPlayerA.jumpViewConeFunc       	= ViewConeTight
    dataForPlayerA.firstPersonJumpOutSound 	= "commander_sequence_soldier_a_jump"



	DropShipSequenceDate dataForPlayerB
    dataForPlayerB.idleAnim           		= "Classic_MP_flyin_exit_playerB_idle"
    dataForPlayerB.idlePOVAnim        		= "Classic_MP_flyin_exit_povB_idle"
	dataForPlayerB.idleAttachment			= "ORIGIN"
	dataForPlayerB.idleTeleport 			= true
	dataForPlayerB.idleHideProxy 			= true
    dataForPlayerB.idleAngles           	= < 0, 8.0, 0 >
	dataForPlayerB.idleViewConeFunc       	= ViewConeRampFree

	dataForPlayerB.jumpAnim           		= "Classic_MP_flyin_exit_playerB_jump"
    dataForPlayerB.jumpPOVAnim        		= "Classic_MP_flyin_exit_povB_jump"
	dataForPlayerB.jumpAttachment			= "ORIGIN"
	dataForPlayerB.jumpTeleport 			= false
	dataForPlayerB.jumpHideProxy 			= true
	dataForPlayerB.jumpViewConeFunc       	= ViewConeTight
    dataForPlayerB.firstPersonJumpOutSound 	= "commander_sequence_soldier_b_jump"



	DropShipSequenceDate dataForPlayerC
    dataForPlayerC.idleAnim           		= "Classic_MP_flyin_exit_playerC_idle"
    dataForPlayerC.idlePOVAnim        		= "Classic_MP_flyin_exit_povC_idle"
	dataForPlayerC.idleAttachment			= "ORIGIN"
	dataForPlayerC.idleTeleport 			= true
	dataForPlayerC.idleHideProxy 			= true
    dataForPlayerC.idleAngles           	= < 0, 8.0, 0 >
	dataForPlayerC.idleViewConeFunc       	= ViewConeRampFree

	dataForPlayerC.jumpAnim           		= "Classic_MP_flyin_exit_playerC_jump"
    dataForPlayerC.jumpPOVAnim        		= "Classic_MP_flyin_exit_povC_jump"
	dataForPlayerC.jumpAttachment			= "ORIGIN"
	dataForPlayerC.jumpTeleport 			= false
	dataForPlayerC.jumpHideProxy 			= true
	dataForPlayerC.jumpViewConeFunc       	= ViewConeTight
    dataForPlayerC.firstPersonJumpOutSound 	= "commander_sequence_soldier_c_jump"



	DropShipSequenceDate dataForPlayerD
    dataForPlayerD.idleAnim           		= "Classic_MP_flyin_exit_playerD_idle"
    dataForPlayerD.idlePOVAnim        		= "Classic_MP_flyin_exit_povD_idle"
	dataForPlayerD.idleAttachment			= "ORIGIN"
	dataForPlayerD.idleTeleport 			= true
	dataForPlayerD.idleHideProxy 			= true
    dataForPlayerD.idleAngles           	= < 0, -16.0, 0 >
	dataForPlayerD.idleViewConeFunc       	= ViewConeRampFree

	dataForPlayerD.jumpAnim           		= "Classic_MP_flyin_exit_playerD_jump"
    dataForPlayerD.jumpPOVAnim        		= "Classic_MP_flyin_exit_povD_jump"
	dataForPlayerD.jumpAttachment			= "ORIGIN"
	dataForPlayerD.jumpTeleport 			= false
	dataForPlayerD.jumpHideProxy 			= true
	dataForPlayerD.jumpViewConeFunc       	= ViewConeTight
    dataForPlayerD.firstPersonJumpOutSound 	= "commander_sequence_soldier_d_jump"



    file.dropShipSequenceDateList = [ dataForPlayerA, dataForPlayerB, dataForPlayerC, dataForPlayerD ]
}

void function DropshipIntro_OnClientConnected( entity player )
{
	file.playerSpawnFromDropship[ player ] <- false
	if ( GetGameState() == eGameState.Prematch )
	{
		// if dropship spawns are not good for this mode, just spawn players into droppod
		// also for very late joiner
		if ( file.validDropshipSpawns.len() != 4 || file.introStartTime + SPAWN_IN_DROPPOD_TIME < Time() )
			SpawnFromDroppod( player, 15 ) // longer lifetime for intro droppods
		else
			thread SpawnPlayerIntoDropship( player )
	}
}

void function OnGameStart()
{
	foreach ( entity player in GetPlayerArray() )
	{
		if ( file.playerSpawnFromDropship[ player ] ) // this player mark as spawn from dropship?
			continue
		
		// otherwise they spawning from droppod
		if ( !IsValid( player.GetParent() ) ) // not respawning from droppod
			player.UnfreezeControlsOnServer() // give mobility, if they're respawning from droppod they'll be handled by it's function
		
		// do gamemode announcement
		thread WaitForPlayerDisembarksDroppod( player )
	}
}

void function WaitForPlayerDisembarksDroppod( entity player )
{
	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )
	
	while ( IsValid( player.GetParent() ) )
		WaitFrame()
	TryGameModeAnnouncement( player )
}

void function OnPrematchStart()
{
	ClassicMP_OnIntroStarted()

	print( "starting gruntmode intro!" )
	file.introStartTime = Time()
	
	// make 2 empty dropship structs per team
	IntroDropship emptyDropship
	file.militiaDropships.clear()
	file.imcDropships.clear()
	
	// if dropship spawns are not good for this mode, just spawn players in droppod
	if ( file.validDropshipSpawns.len() != 4 )
	{
		foreach ( entity player in GetPlayerArray() )
		{
			if ( !IsPrivateMatchSpectator( player ) )
				SpawnFromDroppod( player, 15 ) // longer lifetime for intro droppods
			else
				RespawnPrivateMatchSpectator( player )
		}

		thread EndIntroWhenFinished()
		return
	}
	
	// spawn dropships
	foreach ( entity dropshipSpawn in file.validDropshipSpawns )
	{
		// todo: possibly make this only spawn dropships if we've got enough players to need them
		int createTeam = ( IsSwitchSidesBased() && HasSwitchedSides() == 1 ) ? GetOtherTeam( dropshipSpawn.GetTeam() ) : dropshipSpawn.GetTeam() 
		array<IntroDropship> teamDropships = createTeam == TEAM_MILITIA ? file.militiaDropships : file.imcDropships
		
		if ( teamDropships.len() >= 2 )
			continue

		// create entity
		entity dropship = CreateDropship( createTeam, dropshipSpawn.GetOrigin(), dropshipSpawn.GetAngles() )
		SetSpawnOption_AISettings( dropship, "npc_dropship_hero" ) // proper aisettings

		teamDropships.append( clone emptyDropship )
		teamDropships[ teamDropships.len() - 1 ].dropship = dropship
		
		AddAnimEvent( dropship, "dropship_warpout", WarpoutEffect )
		// as respawn's settings, IMC team goes default
		dropship.SetValueForModelKey( $"models/vehicle/goblin_dropship/goblin_dropship_hero.mdl" )
		dropship.SetModel( $"models/vehicle/goblin_dropship/goblin_dropship_hero.mdl" ) 
		
		DispatchSpawn( dropship )
		
		// have to do this after dispatch otherwise it won't work for some reason
		// weirdly enough, tf2 actually does use different dropships for imc and militia, despite these concepts not really being a thing for players in tf2
		// probably was just missed by devs, but keeping it in for accuracy
		if ( dropshipSpawn.GetTeam() == TEAM_MILITIA )
			dropship.SetModel( $"models/vehicle/crow_dropship/crow_dropship_hero.mdl" )
		else // as respawn's settings, IMC team goes default
			dropship.SetModel( $"models/vehicle/goblin_dropship/goblin_dropship_hero.mdl" )
		
		thread PlayAnim( dropship, "dropship_classic_mp_flyin" )
		// hardcoded for now
		dropship.Anim_SetInitialTime( 7.0 )//( DROPSHIP_INTRO_LENGTH - DROPSHIP_INTRO_LENGTH_SHORTER ) // start with door opened
	}
	
	foreach ( entity player in GetPlayerArray() )
	{
		if ( !IsPrivateMatchSpectator( player ) )
			thread SpawnPlayerIntoDropship( player )
		else
			RespawnPrivateMatchSpectator( player )
	}
	
	thread EndIntroWhenFinished()
}

void function EndIntroWhenFinished()
{
	wait DROPSHIP_INTRO_LENGTH_SHORTER
	ClassicMP_OnIntroFinished()
}

void function SpawnPlayerIntoDropship( entity player )
{
	player.EndSignal( "OnDestroy" )

	if ( IsAlive( player ) )
		player.Die() // kill them so we don't have any issues respawning them later
	
	file.playerSpawnFromDropship[ player ] = true // mark this player as spawning from dropship

	table results = 
	{
		isJumping = false,
		actionSetBusy = false
	}
	
	OnThreadEnd( function() : ( player, results )
	{	
		if ( IsValid( player ) )
		{
			player.ClearParent()
			ClearPlayerAnimViewEntity( player )
			if ( !IsAlive( player ) )
				player.Anim_Stop() // stop sequence for dead players
			
			if ( !results.isJumping )
			{
				player.EnableWeaponViewModel()
				RemoveCinematicFlag( player, CE_FLAG_CLASSIC_MP_SPAWNING )

				if ( results.actionSetBusy )
					player.ContextAction_ClearBusy() // clean up
			}
		}
	})
	
	WaitFrame() // do a waitFrame() for other respawn functions set up

	player.EndSignal( "OnDeath" )
	
	// find the player's dropship and seat
	array<IntroDropship> teamDropships
	if ( player.GetTeam() == TEAM_MILITIA )
		teamDropships = file.militiaDropships
	else
		teamDropships = file.imcDropships
	
	IntroDropship playerDropship
	int playerDropshipIndex = -1
	foreach ( IntroDropship dropship in teamDropships )
	{
		for ( int i = 0; i < dropship.players.len(); i++ )
		{
			if ( dropship.players[ i ] == null )
			{
				playerDropship = dropship
				playerDropshipIndex = i
				
				dropship.players[ i ] = player
				break
			}
		}
	}
					
	if ( playerDropship.dropship == null )
	{
		// if we're at this point, we have more players than we do dropships, so just pick a random one
		playerDropship = teamDropships.getrandom()
		playerDropshipIndex = RandomInt( MAX_DROPSHIP_PLAYERS )
	}
	
	// respawn player and holster their weapons so they aren't out
	DoRespawnPlayer( player, null )
	HolsterViewModelAndDisableWeapons( player ) // disabling viewmodel goes first
	
	// hide hud and fade screen out from black
	AddCinematicFlag( player, CE_FLAG_CLASSIC_MP_SPAWNING )
	ScreenFadeFromBlack( player, 3.0, 1.0 )
	// faction leaders are done clientside, spawn them here
    // default intro is 15s, I want to make it a 5s, so make factionleader's animations etc be 10s faster
	float timeDiff = DROPSHIP_INTRO_LENGTH - DROPSHIP_INTRO_LENGTH_SHORTER // get differ, add 0.1s fix
	// actually don't spawn a commander could be better
	//Remote_CallFunction_NonReplay( player, "ServerCallback_SpawnFactionCommanderInDropship", playerDropship.dropship.GetEncodedEHandle(), file.introStartTime )

	// get current sequence data
	DropShipSequenceDate data = file.dropShipSequenceDateList[ playerDropshipIndex ]

	// mark as player action busy, so other scripts can check it
	if ( !player.ContextAction_IsBusy() )
	{
		player.ContextAction_SetBusy() 
		results.actionSetBusy = true
	}

	// do firstperson idle sequence
	FirstPersonSequenceStruct idleSequence
	idleSequence.firstPersonAnim 	= data.idlePOVAnim
	idleSequence.thirdPersonAnim 	= data.idleAnim
	idleSequence.viewConeFunction 	= data.idleViewConeFunc
	idleSequence.attachment 		= data.idleAttachment
	idleSequence.teleport 			= data.idleTeleport
	idleSequence.hideProxy 			= data.idleHideProxy
	idleSequence.angles				= data.idleAngles
	idleSequence.setInitialTime 	= Time() - file.introStartTime + timeDiff - 1.0 // hardcoded for now, make player ready to jump
	thread FirstPersonSequence( idleSequence, player, playerDropship.dropship )
	float duration = player.GetSequenceDuration( idleSequence.thirdPersonAnim )
	wait duration - timeDiff // for running out from dropship

	// do jumping sequence along with jumping think
	FirstPersonSequenceStruct jumpSequence
	jumpSequence.firstPersonAnim 	= data.jumpPOVAnim
	jumpSequence.thirdPersonAnim 	= data.jumpAnim
	jumpSequence.viewConeFunction 	= data.jumpViewConeFunc
	jumpSequence.attachment 		= data.jumpAttachment
	jumpSequence.teleport 			= data.jumpTeleport
	jumpSequence.hideProxy 			= data.jumpHideProxy
	jumpSequence.setInitialTime 	= max( 0.0, Time() - ( file.introStartTime + 10.0 ) )  // pretty sure you should do this with GetScriptedAnimEventCycleFrac?
	
	// jumping sound
	EmitSoundOnEntityOnlyToPlayer( player, player, data.firstPersonJumpOutSound )
	PlayBattleChatterLine( player, "bc_pIntroChat" )
	thread FirstPersonSequence( jumpSequence, player, playerDropship.dropship )
	WaittillAnimDone( player ) // somehow this is better than just waiting for the blocking FirstPersonSequence call?
	
	results.isJumping = true
	thread PlayerJumpsFromDropship( player )
}

void function PlayerJumpsFromDropship( entity player )
{
	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )

	int moveLockEffect = StatusEffect_AddEndless( player, eStatusEffect.move_slow, 1.0 ) // 1.0 will lock player's movement

	OnThreadEnd
	( 
		function() : ( player, moveLockEffect )
		{
			if ( IsValid( player ) )
			{
				// defensive fix, restore movements
				StatusEffect_Stop( player, moveLockEffect )
				// show weapon viewmodel and hud
				DeployViewModelAndEnableWeapons( player )
				RemoveCinematicFlag( player, CE_FLAG_CLASSIC_MP_SPAWNING )
				if ( player.ContextAction_IsBusy() )
					player.ContextAction_ClearBusy() // clean up busy state set by SpawnPlayerIntoDropship()

				if ( IsAlive( player ) ) // not resetting through death
				{
					RestorePlayerPermanentGravity( player ) // restore gravity
					if ( Loadouts_CanGivePilotLoadout( player ) ) // to fit vanilla: give loadout upon touched ground, so they'll have weapon's first deploy animation
						Loadouts_TryGivePilotLoadout( player )
					else // fix for northstar: we may disabled loadout grade period, should update their weapon to make first deploy animation show up
					{
						StorePilotWeapons( player )
						RetrievePilotWeapons( player )
					}
				}
				else
					player.Anim_Stop() // stop sequence for dead players
			}
		}
	)
		
	// wait for intro timer to be fully done
	wait ( file.introStartTime + DROPSHIP_INTRO_LENGTH_SHORTER ) - Time()
    //player.kv.gravity = 2.2 // make players fall much faster

	// wait for player to hit the ground
	wait 0.1 // assume players will never actually hit ground before this
	player.ConsumeDoubleJump() // disable double jump

	int tickRate = GetConVarInt( "script_server_fps" )
	bool tickRateModified = tickRate > 10
	if ( tickRateModified )
		wait 0.1 // wait an extra 0.1s if tickrate modified!
	player.SetVelocity( < 0, 0, -200 > ) // better visual for match the jumping sequence

	while ( !player.IsOnGround() && !player.IsWallRunning() && !player.IsWallHanging() ) // todo this needs tweaking
	{
		// we remove the movelock when player getting close to ground, so they won't feel dizzy upon first move
		if ( IsPlayerNearGround( player, 350 ) ) // higher checkHeight since we've give player higher speed to drop down
			StatusEffect_Stop( player, moveLockEffect )

		WaitFrame()
	}

	TryGameModeAnnouncement( player )
}

bool function IsPlayerNearGround( entity player, float checkHeight = 128 )
{
	array<entity> ignoreEnts
	// always ignore all npcs and players, try to hit onto ground
	ignoreEnts.extend( GetPlayerArray() )
	ignoreEnts.extend( GetNPCArray() )

	vector traceStart = player.GetOrigin()
	vector traceEnd = traceStart - < 0, 0, checkHeight >

	TraceResults downTrace = TraceLine( traceStart, traceEnd, ignoreEnts, TRACE_MASK_SHOT, TRACE_COLLISION_GROUP_NONE )
	entity hitEnt = downTrace.hitEnt
	if ( !IsValid( hitEnt ) ) // we cannot trace any entity
		return false
	if ( !hitEnt.IsWorld() ) // we're not traced onto ground
		return false

	return true
}