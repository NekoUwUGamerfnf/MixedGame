untyped
global function Sv_EarnMeterMP_Init
global function EarnMeterMP_SetTitanLoadout
global function EarnMeterMP_SetPassiveMeterGainEnabled
global function EarnMeterMP_SetBoostByRef

// modified settings...
global function EarnMeterMP_SetPassiveGainProgessEnable

// when client display a 99% frac, server actually has "0.989999"
// needs to add a small amount to avoid earnmeter displays as 99% but player can't earn goal
// this also fixes player can't gain reward when their meter is right at required value
const float EARNMETER_FRAC_FIX				= 0.0001

const float EARNMETER_GAIN_FRAC				= 0.01 + EARNMETER_FRAC_FIX
const float EARNMETER_GAIN_INTERVAL			= 5.0
const float EARNMETER_GAIN_INTERVAL_MIN		= 1.0
const float EARNMETER_GAIN_MAX_LEVEL		= 0.9 // if time reaches this percentage, we will use EARNMETER_GAIN_INTERVAL_MIN

struct
{
	// these are purely in-file and no need to change
	array<string> catchedUpUIDs // for northstar specific
	// in vanilla, only passive gain triggers EarnMeter_DecayHold()
	table<entity, float> playerLastPassiveGainTime
	// should store player's earnMeter value on titan transfer
	table< entity, table<string, float> > playerSavedPilotEarnMeterValues
	//

	float playingStartTime
	bool passiveMeterGainEnabled = true

	// vanilla behavior settings...
	float passiveMeterGainInterval = EARNMETER_GAIN_INTERVAL
	bool passiveGainProgressEnabled = false // vanilla seems only enable this in few gamemodes( mostly titan allowed modes )
} file

void function Sv_EarnMeterMP_Init()
{
	if ( !EARNMETER_ENABLED )
		return

	// shouldn't use SetupPlayerEarnMeter on connected
	//AddCallback_OnClientConnected( SetupPlayerEarnMeter )
	AddCallback_OnClientConnected( OnClientConnected )
	AddCallback_GameStateEnter( eGameState.Playing, OnPlaying ) // can't change boost after prematch
	AddCallback_OnPlayerRespawned( OnPlayerRespawned )
	// move here from sv_earn_meter.gnut
	RegisterSignal( "EarnMeterDecayThink" )
	RegisterSignal( "OverDriveClearOnDeath" )
	// progressing earnmeter frac
	EarnMeterMP_ProgessFracCalculate()
}

void function EarnMeterMP_SetTitanLoadout( entity player )
{
	if ( EarnMeterMP_IsTitanEarnGametype() )
		PlayerEarnMeter_SetGoal( player, EarnObject_GetByRef( GetTitanLoadoutForPlayer( player ).titanClass ) )
	else
		PlayerEarnMeter_SetGoal( player, PlayerEarnMeter_GetReward( player ) )
}

// modified settings...
void function EarnMeterMP_SetPassiveMeterGainEnabled( bool enabled )
{
	file.passiveMeterGainEnabled = enabled
}

void function EarnMeterMP_SetPassiveGainProgessEnable( bool enabled )
{
	file.passiveGainProgressEnabled = enabled
}
//

void function OnClientConnected( entity player )
{
	SetupPlayerEarnMeter( player )

	// catchup bonus for late joiners
	thread DelayedCatchUpEarnMeter( player )
	// note: needs to delay otherwise it won't work
	// todo: maths on this is fine but for some reason it won't set correctly, could be getting reset somewhere?
	// PlayerEarnMeter_AddOwnedFrac( player, ( ( Time() - file.playingStartTime ) / 4.0 ) * 0.01 )

	// vanilla behavior recover
	file.playerLastPassiveGainTime[ player ] <- 0.0
	table<string, float> earnMeterValues = 
	{
		[ EARNMETER_OWNEDFRAC ] = 0.0,
		[ EARNMETER_EARNEDFRAC ] = 0.0,
	}
	file.playerSavedPilotEarnMeterValues[ player ] <- earnMeterValues
}

void function SetupPlayerEarnMeter( entity player )
{
	PlayerEarnMeter_Reset( player )

	string burncardRef = GetSelectedBurnCardRef( player )
	EarnMeterMP_SetBoostByRef( player, burncardRef )
}

void function DelayedCatchUpEarnMeter( entity player )
{
	WaitFrame() // wait for earnmeter being reset
	// for northstar, since players can re-connect very easily, we use catchedUpUIDs for storing
	if( IsValid( player ) && !file.catchedUpUIDs.contains( player.GetUID() ) )
	{
		PlayerEarnMeter_AddOwnedFrac( player, ( ( Time() - file.playingStartTime ) / EARNMETER_GAIN_INTERVAL ) * EARNMETER_GAIN_FRAC )
		file.catchedUpUIDs.append( player.GetUID() ) // for northstar, only catch up earnmeters once
	}
}

void function OnPlaying()
{
	file.playingStartTime = Time()
	foreach ( entity player in GetPlayerArray() )
		SetupPlayerEarnMeter( player )

	if ( Riff_BoostAvailability() != eBoostAvailability.Disabled )
		SetCallback_EarnMeterRewardEarned( EarnMeterMP_BoostEarned )

	// do this in playing so that gamemodes/maps can disable and this'll take affect
	if ( EarnMeterMP_IsTitanEarnGametype() ) // settitanavailable when earnmeter full
	{
		Riff_ForceTitanAvailability( eTitanAvailability.Custom ) // doesn't seem to affect anything aside from preventing some annoying client stuff
		svGlobal.titanAvailabilityCheck = IsTitanAvailable
	}

	SetCallback_EarnMeterGoalEarned( EarnMeterMP_TitanEarned )
}

void function OnPlayerRespawned( entity player )
{
	thread EarnMeterMP_PlayerLifeThink( player )

	if ( PlayerEarnMeter_IsRewardAvailable( player ) )
		EarnMeterMP_BoostEarned( player )
}

void function EarnMeterMP_ReplaceReward( entity player, EarnObject reward, float rewardFrac )
{
	// saved current earnmeter value if titan transfer
	// so we can show reward discount animatioin on reset
	if ( player.IsTitan() )
	{
		file.playerSavedPilotEarnMeterValues[ player ][ EARNMETER_OWNEDFRAC ] = PlayerEarnMeter_GetOwnedFrac( player )
		file.playerSavedPilotEarnMeterValues[ player ][ EARNMETER_EARNEDFRAC ] = PlayerEarnMeter_GetEarnedFrac( player )
	}
	else
	{
		PlayerEarnMeter_SetOwnedFrac( player, file.playerSavedPilotEarnMeterValues[ player ][ EARNMETER_OWNEDFRAC ] )
		PlayerEarnMeter_SetEarnedFrac( player, file.playerSavedPilotEarnMeterValues[ player ][ EARNMETER_EARNEDFRAC ] )
	}

	PlayerEarnMeter_Reset( player )
	//PlayerEarnMeter_Empty( player )
	if ( reward.id < 0 )
		return
	
	// fix reward icon
	if ( Riff_BoostAvailability() != eBoostAvailability.Disabled )
	{
		PlayerEarnMeter_SetReward( player, reward )
		PlayerEarnMeter_SetRewardFrac( player, rewardFrac )
		if( PlayerEarnMeter_GetRewardFrac( player ) != 0 )
			PlayerEarnMeter_EnableReward( player )
	}
}

void function EarnMeterMP_PlayerLifeThink( entity player )
{
	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )

	EarnObject pilotReward = PlayerEarnMeter_GetReward( player ) 
	float pilotRewardFrac = PlayerEarnMeter_GetRewardFrac( player )
	int lastEarnMeterMode = PlayerEarnMeter_GetMode( player )

	// on respawn: If titan avaliable and we're nearly reach it, do a dialogue
	TryTitanSoonAnnouncement( player )
	// move here from sv_earn_meter.gnut
	// in vanilla, only passive gain triggers EarnMeter_DecayHold()
	// which means you'll drop less overdrive when the match is near end
	thread EarnMeterDecayThink( player )

	OnThreadEnd(
		function() : ( player, pilotReward, pilotRewardFrac )
		{
			if ( !IsValid( player ) )
				return

			// Resets the meter to the pilot version if the player dies in a titan or while their titan is alive (otherwise they can be stuck with e-smoke)
			int earnMode = PlayerEarnMeter_GetMode( player )
			if( earnMode != eEarnMeterMode.DEFAULT )
				EarnMeterMP_ReplaceReward( player, pilotReward, pilotRewardFrac )
		}
	)

	while ( true )
	{
		if( GetGameState() < eGameState.Playing ) // Prematch
			file.playerLastPassiveGainTime[ player ] = Time() // reset gain timer
		
		int desiredEarnMeterMode
		entity petTitan = player.GetPetTitan()
		entity soul = player.GetTitanSoul() // move out of if-else cases

		if ( player.IsTitan() )
		{
			file.playerLastPassiveGainTime[ player ] = Time() // staying in titan should reset earnmeter gain timer
			
			if ( IsValid( soul ) ) // stupid anti-crash, possibly no need
			{
				//entity soul = player.GetTitanSoul() // move out of if-else cases
				if ( SoulTitanCore_GetExpireTime( soul ) > Time() )
					desiredEarnMeterMode = eEarnMeterMode.CORE_ACTIVE
				else
					desiredEarnMeterMode = eEarnMeterMode.CORE
			}
		}
		//else if ( IsValid( player.GetPetTitan() ) )
		else if ( IsValid( petTitan ) )
		{
			file.playerLastPassiveGainTime[ player ] = Time() // having a pet titan should reset earnmeter gain timer
			desiredEarnMeterMode = eEarnMeterMode.PET
		}
		else
			desiredEarnMeterMode = eEarnMeterMode.DEFAULT

		if ( desiredEarnMeterMode != lastEarnMeterMode )
		{
			PlayerEarnMeter_SetMode( player, desiredEarnMeterMode )
			if ( lastEarnMeterMode == eEarnMeterMode.DEFAULT ) // Set these here in case the player changed boost during the match (e.g. in dropship)
			{
				pilotReward = PlayerEarnMeter_GetReward( player ) 
				pilotRewardFrac = PlayerEarnMeter_GetRewardFrac( player )
			}

			if ( desiredEarnMeterMode == eEarnMeterMode.DEFAULT ) // Only occurs when auto titan dies. Resets reward progress and reverts it back to boost.
			{
				print( "updating reward and goal" )
				// update earnmeter
				EarnMeterMP_ReplaceReward( player, pilotReward, pilotRewardFrac )
				// goal is always enabled so the game can refresh it
				PlayerEarnMeter_EnableGoal( player )
			}
			else if ( desiredEarnMeterMode == eEarnMeterMode.CORE ) // Replace the pilot's earn meter reward with smoke when they enter their titan.
			{
				EarnMeterMP_ReplaceReward( player, EarnObject_GetByRef( "core_electric_smoke" ), CORE_SMOKE_FRAC )
				//if( SoulTitanCore_GetNextAvailableTime( player.GetTitanSoul() ) >= CORE_SMOKE_FRAC )
				if ( IsValid( soul ) ) // stupid anti-crash, possibly no need
				{
					// modified here: if titan being disabled reward, we remove their smoke icon
					if( SoulTitanCore_GetNextAvailableTime( soul ) >= CORE_SMOKE_FRAC || !PlayerEarnMeter_GetSoulEarnMeterSmokeEnabled( soul ) )
						PlayerEarnMeter_SetRewardUsed( player )
				}
			}
			else if ( desiredEarnMeterMode == eEarnMeterMode.CORE_ACTIVE ) // Enables smoke after core use (doesn't show up during active, so looks fine)
			{
				// shouldn't show electric smoke icon if titan has PAS_HYPER_CORE, but whatever I don't want to fix it
				// this is a vanilla bug... weird
				// now tried to fix it in sh_titancore_utility.gnut, call a PlayerEarnMeter_SetRewardUsed() after core use
				PlayerEarnMeter_EnableReward( player )
			}

			lastEarnMeterMode = desiredEarnMeterMode
		}

		// below is default earnmeter looping case
		if ( lastEarnMeterMode == eEarnMeterMode.DEFAULT )
		{
			// Removed... client doesn't seem to show "titan ready" in goal ready case
			// the alerts only shown when SetTitanAvailable() is used
			// looping to refresh goal seems bad
			/*
			if ( PlayerEarnMeter_GetOwnedFrac( player ) < 1.0 )
				PlayerEarnMeter_DisableGoal( player )
			else if ( player.GetPlayerNetInt( "goalState" ) != eRewardState.UNAVAILABLE )
			{
				// if goal is enabled then the client will show "titan ready" alerts even if it isn't
				// the problem is that if the goal isn't available when we fill the earnmeter, then it won't make it available
				// so unfortunately we have to do this manually
				player.SetPlayerNetInt( "goalState", eRewardState.AVAILABLE )
				PlayerEarnMeter_RefreshGoal( player )
			}
			*/

			if ( Time() - file.playerLastPassiveGainTime[ player ] >= file.passiveMeterGainInterval && file.passiveMeterGainEnabled ) // this might be 5.0
			{
				PlayerEarnMeter_AddOwnedFrac( player, EARNMETER_GAIN_FRAC )
				file.playerLastPassiveGainTime[ player ] = Time() // update last passive gain time
			}
		}

		WaitFrame()
	}
}

void function TryTitanSoonAnnouncement( entity player )
{
	//WaitFrame() // required for sometimes player try "RespawnAsTitan()", was fixed inside it
	if( !IsValid( player ) )
		return
	if( ShouldDoTitanSoonAnnouncement( player ) )
	{
		//print( "doing titan soon announcement" )
		PlayFactionDialogueToPlayer( "mp_titanSoon", player )
	}
}

bool function ShouldDoTitanSoonAnnouncement( entity player )
{
	if( 
		EarnMeterMP_IsTitanEarnGametype() && 
		PlayerEarnMeter_GetOwnedFrac( player ) >= 0.8 && 
		!IsTitanAvailable( player ) && 
		!player.IsTitan() && 
		!IsValid( player.GetPetTitan() ) &&
		GetGameState() == eGameState.Playing
	  )
		return true

	return false
}

void function EarnMeterMP_BoostEarned( entity player )
{
	// Can't have smoke earned via meter. Otherwise, Auto Titan could hit reward frac and get nothing
	if( player.IsTitan() )
		return

	EarnObject earnobject = PlayerEarnMeter_GetReward( player )
	BurnReward burncard = BurnReward_GetByRef( earnobject.ref )

	while ( burncard.ref == "burnmeter_random_foil" )
		burncard = BurnReward_GetRandom()

	// rework northstar complicated checks. why don't we just get reward count before applying?
	int rewardCount = BurnMeter_GetLimitedRewardCount( player, burncard.ref )
	//for ( int i = 0; i < BurnMeter_GetLimitedRewardCount( player, burncard.ref ); i++ )
	for ( int i = 0; i < rewardCount; i++ )
		BurnMeter_GiveRewardDirect( player, burncard.ref )

	PlayerEarnMeter_DisableReward( player )
}

void function EarnMeterMP_TitanEarned( entity player )
{
	if ( EarnMeterMP_IsTitanEarnGametype() )
	{
		if( !player.IsTitan() && !IsValid( player.GetPetTitan() ) && !IsTitanAvailable( player ) ) // sometimes trigger this when "Respawn As Titan", should do a check.
		{
			SetTitanAvailable( player )
			Remote_CallFunction_Replay( player, "ServerCallback_TitanReadyMessage" ) // broken for some reason
		}
	}
	else
	{
		EarnMeterMP_ResetPilotRewardState( player )
	}
}

void function EarnMeterMP_ResetPilotRewardState( entity player )
{
	float oldRewardFrac = PlayerEarnMeter_GetRewardFrac( player ) // get reward frac before it gets reset
	// shouldn't use reset as it breaks reward and goal state
	//PlayerEarnMeter_Reset( player )
	PlayerEarnMeter_Empty( player )

	// don't enable reward if we've disabled it, otherwise the icon will be shown incorrectly
	if ( Riff_BoostAvailability() != eBoostAvailability.Disabled )
	{
		if ( oldRewardFrac > 0 )
		{
			PlayerEarnMeter_SetRewardFrac( player, oldRewardFrac )
			PlayerEarnMeter_EnableReward( player )
		}
	}
	// goal is always enabled so the game can refresh it
	PlayerEarnMeter_EnableGoal( player )
}

void function EarnMeterMP_SetBoostByRef( entity player, string boostRef ) 
{
	EarnObject earnobject = EarnObject_GetByRef( boostRef )
	BurnReward burncard = BurnReward_GetByRef( boostRef )

	if ( Riff_BoostAvailability() != eBoostAvailability.Disabled )
	{
		PlayerEarnMeter_SetReward( player, earnobject ) // pretty sure this works?
		PlayerEarnMeter_SetRewardFrac( player, burncard.cost )
		PlayerEarnMeter_EnableReward( player )
	}

	// goal is always enabled so the game can refresh it
	PlayerEarnMeter_EnableGoal( player )

	if ( EarnMeterMP_IsTitanEarnGametype() )
	{
		PlayerEarnMeter_SetGoal( player, EarnObject_GetByRef( GetTitanLoadoutForPlayer( player ).titanClass ) )
		//PlayerEarnMeter_EnableGoal( player ) // prevents goalstate from being set incorrectly
	}
	else
		PlayerEarnMeter_SetGoal( player, earnobject )
}

// move here from sv_earn_meter.gnut
// in vanilla, only passive gain triggers EarnMeter_DecayHold()
// which means you'll drop less overdrive when the match is near end
void function EarnMeterDecayThink( entity player )
{
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )
	player.Signal( "EarnMeterDecayThink" ) // prevent this instance running multiple times
	player.EndSignal( "EarnMeterDecayThink" )

	thread OverDriveClearOnDeath( player )

	// 10% over 20 seconds
	float decayRate = ( 1.0 / 135.0 ) * 0.1 // 0.1 is for 1tick
	//float startTime = Time()
	while ( true )
	{
		WaitFrame() // wait before each loop
		//float frameTime = Time() - startTime
		//startTime = Time()

		entity titan = GetTitanFromPlayer( player )
		if ( IsValid( titan ) ) // wait for titan being destroyed
			continue

		// decay hold think
		float decayHoldEndTime = file.playerLastPassiveGainTime[ player ] + EarnMeter_DecayHold() + 0.1 // add 0.1s more for better handle min interval
		// if player in decay hold, we never reduce their earnmeter frac
		if ( Time() < decayHoldEndTime )
			continue

		float earnedValue = PlayerEarnMeter_GetEarnedFrac( player )
		float ownedValue = PlayerEarnMeter_GetOwnedFrac( player )

		if ( earnedValue > ownedValue )
			PlayerEarnMeter_AddEarnedFrac( player, -decayRate )
	}
}

void function OverDriveClearOnDeath( entity player )
{
	player.EndSignal( "OnDestroy" )
	player.Signal( "OverDriveClearOnDeath" ) // prevent this instance running multiple times
	player.EndSignal( "OverDriveClearOnDeath" )
	player.WaitSignal( "OnDeath" )

	float earnedValue = PlayerEarnMeter_GetEarnedFrac( player )
	float ownedValue = PlayerEarnMeter_GetOwnedFrac( player )
	if ( earnedValue > ownedValue )
		PlayerEarnMeter_SetEarnedFrac( player, PlayerEarnMeter_GetOwnedFrac( player ) )
}

// progressFrac calculate
// vanilla seems only enable this in few gamemodes( mostly titan allowed modes such as aitdm and tdm )
void function EarnMeterMP_ProgessFracCalculate()
{
	thread EarnMeterMP_ProgessFracCalculate_Threaded()
}

void function EarnMeterMP_ProgessFracCalculate_Threaded()
{
	// progressFrac calculate
	while ( true )
	{
		WaitFrame() // always wait before each loop

		// no support for roundbased mode
		if ( IsRoundBased() )
		{
			file.passiveMeterGainInterval = EARNMETER_GAIN_INTERVAL
			continue
		}

		// not enabled settings
		if ( !file.passiveGainProgressEnabled )
		{
			file.passiveMeterGainInterval = EARNMETER_GAIN_INTERVAL
			continue
		}

		// in prematch or something
		if ( GetGameState() < eGameState.Playing )
		{
			file.passiveMeterGainInterval = EARNMETER_GAIN_INTERVAL
			continue
		}

		// sudden death or epilogue: always maxnium earnmeter gain
		// vanilla doesn't seem to have this behavior
		// they just keep the value calculated from playing state
		if ( GetGameState() == eGameState.Epilogue || GetGameState() == eGameState.SuddenDeath )
		{
			//file.passiveMeterGainInterval = EARNMETER_GAIN_INTERVAL_MIN
			continue
		}

		// generic calculation while game playing
		if ( GetGameState() == eGameState.Playing )
		{
			float startTime = file.playingStartTime
			// could cache these, but what if we update it midgame?
			float endTime = expect float( GetServerVar( "gameEndTime" ) )
			//if( IsSwitchSidesBased() ) // this has already being calculated in gamestate_mp, don't do it again
			//	endTime /= 2
				
			float totalTime = endTime - startTime
			float maxLevelTime = totalTime * EARNMETER_GAIN_MAX_LEVEL
			
			// vanilla doesn't seem to have progress based on score
			// they only calculate based on time...
			/*
			int totalScore = GameMode_GetScoreLimit( GAMETYPE )
			if( IsRoundBased() )
				totalScore = GameMode_GetRoundScoreLimit( GAMETYPE )
			int maxLevelScore = int( totalScore * EARNMETER_GAIN_MAX_LEVEL )
			*/

			// base frac based on gameTime
			float timeElapsed = Time() - startTime
			float timeLevel = timeElapsed / maxLevelTime
			
			// vanilla doesn't seem to have progress based on score
			// they only calculate based on time...
			/*
			// calculate again based on team scores
			float scoreLevel = 0.0
			if ( !IsSwitchSidesBased() ) // we don't do scoreLevel in switchSideBased modes
			{
				int winningTeam = GetWinningTeamWithFFASupport() // GetWinningTeam() won't be set up if match hasn't end yet
				if ( winningTeam < TEAM_UNASSIGNED ) // no valid winner
					winningTeam = TEAM_UNASSIGNED
				int highestScore = GameRules_GetTeamScore( winningTeam )

				scoreLevel = float( highestScore ) / float( maxLevelScore )
			}
			*/

			// get max level
			// vanilla doesn't seem to have progress based on score
			// they only calculate based on time...
			//float gainLevel = max( timeLevel, scoreLevel )
			float gainLevel = timeLevel
			if ( gainLevel >= 1.0 )
				gainLevel = 1.0
			//print( "gainLevel: " + string( gainLevel ) )
			// get max time reduction
			float maxIntervalReduction = EARNMETER_GAIN_INTERVAL - EARNMETER_GAIN_INTERVAL_MIN

			file.passiveMeterGainInterval = EARNMETER_GAIN_INTERVAL - ( maxIntervalReduction * gainLevel )
			//print( "passiveMeterGainInterval: " + string( file.passiveMeterGainInterval ) )

			continue
		}
	}
}